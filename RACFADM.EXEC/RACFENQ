/*%NOCOMMENT====================* REXX *==============================*/
/*  PURPOSE:  RACFADM - Enqueues - Menu option E (hidden)             */
/*--------------------------------------------------------------------*/
/* FLG  YYMMDD  USERID   DESCRIPTION                                  */
/* ---  ------  -------  -------------------------------------------- */
/* @A6  200412  RACFA    Chg TRACE to allow 'L'abels or 'R'esults     */
/* @A5  200402  RACFA    Chg LRECL=132 to LRECL=80                    */
/* @A4  200402  RACFA    Issue message if no LIBDEF ISPPLIB dataset   */
/* @A3  200401  RACFA    Chged edit macro RACFLOGE to RACFEMAC        */
/* @A2  200401  RACFA    VIEW/EDIT use edit macro, to turn off HILITE */
/* @A1  200402  RACFA    Dropped array, save memory                   */
/* @A0  200401  RACFA    Created REXX                                 */
/*====================================================================*/
PANELM2  = "RACFMSG2"    /* Display RACF command and RC    */
EDITMACR = "RACFEMAC"    /* Edit Macro, turn HILITE off    */ /* @A3 */
QNAME    = "SYSDSN"
DDNAME   = "RACFENQ"

ADDRESS ISPEXEC
  "VGET (ERADISP ERASHOW ERATRACE) ASIS"
  If (eratrace <> 'NO') then do                               /* @A6 */
     parse source . . REXXPGM .                               /* @A6 */
     Say "*"COPIES("-",70)"*"                                 /* @A6 */
     Say "*"Center("Begin Program = "REXXPGM,70)"*"           /* @A6 */
     Say "*"COPIES("-",70)"*"                                 /* @A6 */
     interpret "Trace "SUBSTR(ERATRACE,1,1)                   /* @A6 */
  end                                                         /* @A6 */

  "QLIBDEF ISPPLIB TYPE(DATASET) ID(DSNAME)"
  if (RC > 0) then do                                         /* @A4 */
     call errormsgs ERR19                                     /* @A4 */
     call Goodbye                                             /* @A4 */
  end                                                         /* @A6 */

  RNAME = STRIP(DSNAME,,"'")
  "QUERYENQ TABLE(ENQTBL) QNAME(QNAME) RNAME(RNAME) XSYS"
  "TBTOP   "ENQTBL
  "TBQUERY "ENQTBL" ROWNUM(ENUM)"

  IF (ENUM >= 1) THEN DO
     QUEUE "*"COPIES("-",70)"*"
     QUEUE "*"CENTER("Enqueues - "Rname,70)"*"
     QUEUE "*"COPIES("-",70)"*"
     QUEUE " "
     QUEUE "     Lpar     Disp    Jobname ",
           Copies(" ",12)"Name"
     QUEUE "   --------   -----   --------",
           "  "Copies("-",25)

     DO J = 1 TO ENUM
        "TBSKIP "ENQTBL
        "TBGET  "ENQTBL
        cmd = "LU "ZENJOB
        X = OUTTRAP("CMDREC.")
        ADDRESS TSO cmd
        cmd_rc = rc
        if (erashow <> 'NO') then
           call SHOWCMD
        if (cmd_rc > 0) then
           Luname = ""
        else
           parse var cmdrec.1 . "NAME="luname "OWNER=" .
        X = OUTTRAP("OFF")
        QUEUE "   "LEFT(ZENSYST,8)"   "ZENDISP"  ",
              LEFT(ZENJOB,8)"   "Luname
     END
  END
  "TBCLOSE "ENQTBL
  DROP CMDREC.                                                /* @A1 */
  QUEUE ""

  ADDRESS TSO "ALLOC F("DDNAME") NEW REUSE",
              "LRECL(80) BLKSIZE(0) RECFM(F B)",              /* @A5 */
              "UNIT(VIO) SPACE(1 5) CYLINDERS"
  ADDRESS TSO "EXECIO * DISKW "DDNAME" (FINIS"
  "LMINIT DATAID(CMDDATID) DDNAME("DDNAME")"
  SELECT
     WHEN (ERADISP = "VIEW") THEN
          "VIEW DATAID("CMDDATID") MACRO("EDITMACR")"         /* @A2 */
     WHEN (ERADISP = "EDIT") THEN
          "EDIT DATAID("CMDDATID") MACRO("EDITMACR")"         /* @A2 */
     OTHERWISE
          "BROWSE DATAID("CMDDATID")"
  END
  ADDRESS TSO "FREE FI("DDNAME")"
  call Goodbye                                                /* @A6 */
EXIT
/*--------------------------------------------------------------------*/
/*  Check tracing is on, display flower box                      @A6  */
/*--------------------------------------------------------------------*/
GOODBYE:                                                      /* @A6 */
  If (eratrace <> 'NO') then do                               /* @A6 */
     Say "*"COPIES("-",70)"*"                                 /* @A6 */
     Say "*"Center("End Program = "REXXPGM,70)"*"             /* @A6 */
     Say "*"COPIES("-",70)"*"                                 /* @A6 */
  end                                                         /* @A6 */
EXIT                                                          /* @A6 */
/*--------------------------------------------------------------------*/
/*  Display RACF command and return code                              */
/*--------------------------------------------------------------------*/
SHOWCMD:
  IF (ERASHOW = "BOTH") | (ERASHOW = "DISPLAY") THEN DO
     PARSE VAR CMD MSG1 60 MSG2 121 MSG3
     MSG4 = "Return code = "cmd_rc
     "ADDPOP ROW(6) COLUMN(4)"
     "DISPLAY PANEL("PANELM2")"
     "REMPOP"
  END
  IF (ERASHOW = "BOTH") | (ERASHOW = "LOG") THEN DO
     zerrsm = "RACFADM RACFUSR  RC="cmd_rc
     zerrlm = cmd
     'log msg(isrz003)'
  END
RETURN
