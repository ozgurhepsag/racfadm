/*%NOCOMMENT====================* REXX *==============================*/
/*  PURPOSE:  RACFADM - Enqueues - Menu option E (hidden)             */
/*--------------------------------------------------------------------*/
/* FLG  YYMMDD  USERID   DESCRIPTION                                  */
/* ---  ------  -------  -------------------------------------------- */
/* @AE  200416  RACFA    Display enqueues on RACF database            */
/* @AD  200413  RACFA    Take into account, PROFILE MSGID(ON)         */
/* @AC  200413  RACFA    Del ver word 2 is CLIST, some may be EXEC    */
/* @AB  200413  RACFA    Added more comments above 'GET_CLIST_DSN'    */
/* @AA  200413  RACFA    If ALTLIB, not have 'Appl Lev', skip code    */
/* @A9  200413  RACFA    Get REXX pgm name and use as DD name         */
/* @A8  200413  RACFA    Chg TRACEing to only display banner (P=Pgms) */
/* @A7  200412  RACFA    Add enqueues for msgs and REXX dsns          */
/* @A6  200412  RACFA    Chg TRACE to allow 'L'abels or 'R'esults     */
/* @A5  200402  RACFA    Chg LRECL=132 to LRECL=80                    */
/* @A4  200402  RACFA    Issue message if no LIBDEF ISPPLIB dataset   */
/* @A3  200401  RACFA    Chged edit macro RACFLOGE to RACFEMAC        */
/* @A2  200401  RACFA    VIEW/EDIT use edit macro, to turn off HILITE */
/* @A1  200402  RACFA    Dropped array, save memory                   */
/* @A0  200401  RACFA    Created REXX                                 */
/*====================================================================*/
PANELM2  = "RACFMSG2"    /* Display RACF command and RC    */
EDITMACR = "RACFEMAC"    /* Edit Macro, turn HILITE off    */ /* @A3 */
QNAME    = "SYSDSN"      /* MAJOR resource name for DSNs   */
ENQRC    = 0             /* Verify ALTLIB/LIBDEFs exist    */
parse source . . DDNAME .                                     /* @A9 */

ADDRESS ISPEXEC
  "VGET (ERADISP ERASHOW ERATRACE) ASIS"
  If (eratrace <> 'NO') then do                               /* @A6 */
     parse source . . REXXPGM .                               /* @A6 */
     Say "*"COPIES("-",70)"*"                                 /* @A6 */
     Say "*"Center("Begin Program = "REXXPGM,70)"*"           /* @A6 */
     Say "*"COPIES("-",70)"*"                                 /* @A6 */
     if (eratrace <> 'PROGRAMS') THEN                         /* @A8 */
        interpret "Trace "SUBSTR(ERATRACE,1,1)                /* @A6 */
  end                                                         /* @A6 */

  QUEUE "*"COPIES("-",70)"*"
  QUEUE "*"CENTER("Enqueues",70)"*"
  QUEUE "*"COPIES("-",70)"*"
  call Get_enqs "CLIST"                                       /* @A7 */
  call Get_enqs "ISPPLIB"                                     /* @A7 */
  call Get_enqs "ISPMLIB"                                     /* @A7 */
  call Get_enqs "RACFDB"                                      /* @AE */
  IF (ENQRC = 32) THEN DO                                     /* @AE */
     ADDRESS TSO "DELSTACK"                                   /* @A7 */
     RETURN                                                   /* @A7 */
  END                                                         /* @A7 */
  QUEUE ""

  ADDRESS TSO "ALLOC F("DDNAME") NEW REUSE",
              "LRECL(80) BLKSIZE(0) RECFM(F B)",              /* @A5 */
              "UNIT(VIO) SPACE(1 5) CYLINDERS"
  ADDRESS TSO "EXECIO * DISKW "DDNAME" (FINIS"
  "LMINIT DATAID(CMDDATID) DDNAME("DDNAME")"
  SELECT
     WHEN (ERADISP = "VIEW") THEN
          "VIEW DATAID("CMDDATID") MACRO("EDITMACR")"         /* @A2 */
     WHEN (ERADISP = "EDIT") THEN
          "EDIT DATAID("CMDDATID") MACRO("EDITMACR")"         /* @A2 */
     OTHERWISE
          "BROWSE DATAID("CMDDATID")"
  END
  ADDRESS TSO "FREE FI("DDNAME")"
  call Goodbye                                                /* @A6 */
EXIT
/*--------------------------------------------------------------------*/
/*  If tracing is on, display flower box                         @A6  */
/*--------------------------------------------------------------------*/
GOODBYE:                                                      /* @A6 */
  If (eratrace <> 'NO') then do                               /* @A6 */
     Say "*"COPIES("-",70)"*"                                 /* @A6 */
     Say "*"Center("End Program = "REXXPGM,70)"*"             /* @A6 */
     Say "*"COPIES("-",70)"*"                                 /* @A6 */
  end                                                         /* @A6 */
EXIT                                                          /* @A6 */
/*--------------------------------------------------------------------*/
/*  Obtain enqueues on datasets                                  @A7  */
/*--------------------------------------------------------------------*/
GET_ENQS:                                                     /* @A7 */
  PARSE ARG DDQLIB                                            /* @A7 */
  SELECT                                                      /* @AE */
     WHEN (DDQLIB = "CLIST") THEN                             /* @AE */
          CALL GET_CLIST_DSN                                  /* @A7 */
     WHEN (DDQLIB = "RACFDB") THEN                            /* @AE */
          CALL GET_RACF_DBNAME                                /* @AE */
     OTHERWISE                                                /* @AE */
          "QLIBDEF "DDQLIB" TYPE(DATASET) ID(DSNAME)"         /* @A7 */
  END                                                         /* @AE */
  if (RC > 0) then do                                         /* @A4 */
     call racfmsgs ERR19                                      /* @A4 */
     ENQRC = ENQRC + 8                                        /* @A7 */
     return                                                   /* @A7 */
  end                                                         /* @A6 */

  ROWS  = 0
  RNAME = STRIP(DSNAME,,"'")
  "QUERYENQ TABLE(ENQTBL) QNAME(QNAME) RNAME(RNAME) XSYS"
  QUERYRC = RC
  IF (QUERYRC <> 8) THEN DO                                   /* @AE */
     "TBTOP   "ENQTBL
     "TBQUERY "ENQTBL" ROWNUM(ROWS)"
  END                                                         /* @AE */

  IF (ROWS = 0) THEN DO                                       /* @A7 */
     IF (QUERYRC <> 8) THEN                                   /* @AE */
        "TBCLOSE "ENQTBL                                      /* @A7 */
     QUEUE " "                                                /* @AE */
     QUEUE "  DSN = "Rname"  ",                               /* @AE */
           "Enqueues = "0                                     /* @AE */
     QUEUE " "                                                /* @AE */
     call racfmsgs ERR19                                      /* @A7 */
     ENQRC = ENQRC + 8                                        /* @A7 */
     RETURN                                                   /* @A7 */
  END                                                         /* @A7 */
  ELSE DO                                                     /* @A7 */
     QUEUE " "
     QUEUE "  DSN = "Rname"  ",                               /* @A7 */
           "Enqueues = "strip(ROWS,'L',0)                     /* @A7 */
     QUEUE " "
     QUEUE "         Lpar     Disp    Jobname ",              /* @A7 */
           Copies(" ",12)"Name"
     QUEUE "       --------   -----   --------",              /* @A7 */
           "  "Copies("-",25)

     DO J = 1 TO ROWS
        "TBSKIP "ENQTBL
        "TBGET  "ENQTBL
        cmd = "LU "ZENJOB
        X = OUTTRAP("CMDREC.")
        ADDRESS TSO cmd
        cmd_rc = rc
        if (erashow <> 'NO') then
           call SHOWCMD
        if (cmd_rc > 0) then
           Luname = ""
        else
           parse var cmdrec.1 . "NAME="luname "OWNER=" .
        X = OUTTRAP("OFF")
        QUEUE "       "LEFT(ZENSYST,8)"   "ZENDISP"  ",       /* @A7 */
              LEFT(ZENJOB,8)"   "Luname
     END
  END
  "TBCLOSE "ENQTBL
  QUEUE " "                                                   /* @A7 */
  DROP CMDREC.                                                /* @A1 */
RETURN                                                        /* @A7 */
/*--------------------------------------------------------------------*/
/*  Get CLIST dataset name                                       @A7  */
/*--------------------------------------------------------------------*/
/*  1) The 'ALTLIB DISPLAY' statement, will look for                  */
/*     'Application-level' in the display in order to obtain          */
/*     the DDname of the ALTLIBed dataset                             */
/*       Current search order (by DDNAME) is:                         */
/*       Application-level CLIST DDNAME=SYS00529                      */
/*       System-level EXEC       DDNAME=SYSEXEC                       */
/*       System-level CLIST      DDNAME=SYSPROC                       */
/*  2) The 'LISTA STATUS' will display all the DDnames and datasets   */
/*     allocated, allowing the capability to obtain the dataset name  */
/*     allocated to the 'Application-level CLIST' ddname (SYS#####)   */
/*--------------------------------------------------------------------*/
GET_CLIST_DSN:                                                /* @A7 */
ADDRESS TSO                                                   /* @A7 */
  X = OUTTRAP("REC.")                                         /* @A7 */
  "ALTLIB DISPLAY"                                            /* @A7 */
  X = OUTTRAP("OFF")                                          /* @A7 */
                                                              /* @A7 */
  IF (SUBSTR(REC.2,1,3) = "IKJ") THEN                         /* @AD */
     PARSE VAR REC.2 . W1 W2 "DDNAME="DDALTLIB                /* @AD */
  ELSE                                                        /* @AD */
     PARSE VAR REC.2 W1 W2 "DDNAME="DDALTLIB                  /* @A7 */
  DROP REC.                                                   /* @A7 */
  RC = 0                                                      /* @AA */
  IF (W1 <> "Application-level") THEN DO                      /* @AC */
     RC = 8                                                   /* @AA */
     return                                                   /* @A7 */
  END                                                         /* @AA */
                                                              /* @A7 */
  X = OUTTRAP("REC.")                                         /* @A7 */
  "LISTA STATUS"                                              /* @A7 */
  X = OUTTRAP("OFF")                                          /* @A7 */
  do J = 1 TO REC.0                                           /* @A7 */
     PARSE VAR REC.J W1 .                                     /* @A7 */
     IF (W1 = DDALTLIB) THEN DO                               /* @A7 */
        K = J - 1                                             /* @A7 */
        DSNAME = REC.K                                        /* @A7 */
        LEAVE                                                 /* @A7 */
     END                                                      /* @A7 */
  end                                                         /* @A7 */
  DROP REC. W1 DDALTLIB                                       /* @A7 */
ADDRESS ISPEXEC                                               /* @A7 */
RETURN                                                        /* @A7 */
/*--------------------------------------------------------------------*/
/*  Display RACF command and return code                              */
/*--------------------------------------------------------------------*/
SHOWCMD:
  IF (ERASHOW = "BOTH") | (ERASHOW = "DISPLAY") THEN DO
     PARSE VAR CMD MSG1 60 MSG2 121 MSG3
     MSG4 = "Return code = "cmd_rc
     "ADDPOP ROW(6) COLUMN(4)"
     "DISPLAY PANEL("PANELM2")"
     "REMPOP"
  END
  IF (ERASHOW = "BOTH") | (ERASHOW = "LOG") THEN DO
     zerrsm = "RACFADM RACFUSR  RC="cmd_rc
     zerrlm = cmd
     'log msg(isrz003)'
  END
RETURN
/*--------------------------------------------------------------------*/
/*  Get RACF database name                                       @AE  */
/*--------------------------------------------------------------------*/
GET_RACF_DBNAME:                                              /* @AE */
  cvt      = c2x(storage(10,4))                               /* @AE */
  cvtrac$  = d2x((x2d(cvt))+992)                              /* @AE */
  cvtrac   = c2x(storage(cvtrac$,4))                          /* @AE */
  rcvtsta$ = d2x((x2d(cvtrac))+56)                            /* @AE */
  DSNAME   = STRIP(storage(rcvtsta$,44))                      /* @AE */
  IF (DSNAME = "") THEN                                       /* @AE */
     RC = 8                                                   /* @AE */
RETURN                                                        /* @AE */
