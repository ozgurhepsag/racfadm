/*%NOCOMMENT====================* REXX *==============================*/
/*  PURPOSE:  RACFADM - Settings - Get defualt settings for new users */
/*--------------------------------------------------------------------*/
/*  NOTES:    1) This member will read in $DEFSETG to obtain the      */
/*               default Settings (Option 0) for new users            */
/*            2) To override these settings, create a member called   */
/*               $DEFSITE and define/set any variables                */
/*               - The $DEFSITE member is optional, but if it exists, */
/*                 it will be read and override any settings defined  */
/*                 in member $DEFSETG                                 */
/*               - This will prevent from having to update the        */
/*                 $DEFSETG member everytime a new version of         */
/*                 RACFADM is installed                               */
/*--------------------------------------------------------------------*/
/* FLG  YYMMDD  USERID   DESCRIPTION                                  */
/* ---  ------  -------  -------------------------------------------- */
/* @A7  200523  LBD      Allow a site override mbr, called $DEFSITE   */
/* @A6  200522  RACFA    Added DROP REC.                              */
/* @A5  200519  RACFA    Validate SHOWMVS DSN exit/valid              */
/* @A4  200512  RACFA    Validate IBM RACF DSNs exist/valid           */
/* @A3  200512  RACFA    Moved chking/defining var W3 out of SELECT   */
/* @A2  200511  RACFA    Remove single/double quotes                  */
/* @A1  200511  RACFA    Added SETMIBM                                */
/* @A0  200501  RACFA    Created REXX                                 */
/*====================================================================*/
MBRDSETG    = "$DEFSETG"   /* Default Settings member      */
MBRDSITE    = "$DEFSITE"   /* Site Default Overrides       */ /* @A7 */
DDNAME      = 'RACFA'RANDOM(0,999)  /* Unique ddname       */

ADDRESS ISPEXEC
  'vget (SETMADMN) profile'
  if (SETMADMN <> '') then return  /* Not new user, return */

  "QLIBDEF ISPPLIB TYPE(DATASET) ID(DSNAMEW)"                 /* @A7 */
  if (RC > 0) then
     call RACFMSGS ERR19
  DSNAME  = "'"STRIP(DSNAMEW,,"'")"("MBRDSETG")'"             /* @A7 */
  SITEDSN = "'"STRIP(DSNAMEW,,"'")"("MBRDSITE")'"             /* @A7 */

ADDRESS TSO
  "ALLOC FI("DDNAME") DA("DSNAME") SHR REUSE"
  "EXECIO * DISKR "DDNAME" (STEM REC. FINIS"
  "FREE FI("DDNAME")"

/* ------------------------------- *                          /* @A7 */
 | Check for Site Over-Ride Member |                          /* @A7 */
 | And if so combine into REC. stem|                          /* @A7 */
 * ------------------------------- */                         /* @A7 */
 if (sysdsn(sitedsn) = 'OK') then do                          /* @A7 */
    "ALLOC FI("DDNAME") DA("SITEDSN") SHR REUSE"              /* @A7 */
    "EXECIO * DISKR "DDNAME" (STEM site. FINIS"               /* @A7 */
    "FREE FI("DDNAME")"                                       /* @A7 */
    oc = rec.0                                                /* @A7 */
    do i = 1 to site.0                                        /* @A7 */
       oc = oc + 1                                            /* @A7 */
       rec.oc = site.i                                        /* @A7 */
    end                                                       /* @A7 */
    rec.0 = oc                                                /* @A7 */
  end                                                         /* @A7 */
  drop site.                                                  /* @A7 */

ADDRESS ISPEXEC
  DO J = 1 TO REC.0
     IF (SUBSTR(REC.J,1,1) = "*") THEN ITERATE
     PARSE VAR REC.J W1 W2 W3L W4 .                           /* @A7 */
     w3 = translate(w3l)                                      /* @A7 */
     IF (W3 = "/*") THEN W3L = ""                             /* @A3 */
     IF (W3 = "Y")  THEN W3L = "YES"                          /* @A3 */
     IF (W3 = "N")  THEN W3L = "NO"                           /* @A3 */
     W3 = STRIP(W3L,,"'")                                     /* @A2 */
     W3 = STRIP(W3L,,'"')                                     /* @A2 */
     SELECT
        WHEN (W1 = "SETGDISP") THEN
           SELECT
              WHEN (W3 = "B")  THEN W3L = "BROWSE"            /* @A7 */
              WHEN (W3 = "E")  THEN W3L = "EDIT"              /* @A7 */
              WHEN (W3 = "V")  THEN W3L = "VIEW"              /* @A7 */
              OTHERWISE NOP
           END
        WHEN (W1 = "SETMSHOW") THEN
           SELECT
              WHEN (W3 = "D")  THEN W3L = "DISPLAY"           /* @A7 */
              WHEN (W3 = "L")  THEN W3L = "LOG"               /* @A7 */
              WHEN (W3 = "B")  THEN W3L = "BOTH"              /* @A7 */
              OTHERWISE NOP
           END
        WHEN (W1 = "SETMTRAC") THEN
           SELECT
              WHEN (W3 = "L")  THEN W3L = "LABELS"            /* @A7 */
              WHEN (W3 = "P")  THEN W3L = "PROGRAMS"          /* @A7 */
              WHEN (W3 = "R")  THEN W3L = "RESULTS"           /* @A7 */
              OTHERWISE NOP
           END
        WHEN (W1 = "SETDMVSL") & (W3 <> "") THEN              /* @A5 */
             IF (SYSDSN("'"W3"'") <> "OK") THEN DO            /* @A5 */
                RACFSMSG = "Invalid RACF Msg DSN"             /* @A5 */
                RACFLMSG = "The LINKLIB dataset defined",     /* @A5 */
                           "to variable SETDMVSL does",       /* @A5 */
                           "not exist, DSN="W3".",            /* @A5 */
                           "Please update/fix the site",      /* @A5 */
                           "default settings in panel",       /* @A5 */
                           "member $DEFSETG, delete member",  /* @A5 */
                           "RADMPROF in your ISPF profile",   /* @A5 */
                           "dataset and re-invoke RACFADM."   /* @A5 */
                "SETMSG MSG(RACF011)"                         /* @A5 */
             END                                              /* @A5 */
        WHEN (W1 = "SETDIBMM") & (W3 <> "") THEN              /* @A4 */
             IF (SYSDSN("'"W3"'") <> "OK") THEN DO            /* @A4 */
                RACFSMSG = "Invalid RACF Msg DSN"             /* @A4 */
                RACFLMSG = "The MESSAGE dataset defined",     /* @A4 */
                           "to variable SETDIBMM does",       /* @A4 */
                           "not exist, DSN="W3".",            /* @A4 */
                           "Please update/fix the site",      /* @A4 */
                           "default settings in panel",       /* @A4 */
                           "member $DEFSETG, delete member",  /* @A4 */
                           "RADMPROF in your ISPF profile",   /* @A4 */
                           "dataset and re-invoke RACFADM."   /* @A4 */
                "SETMSG MSG(RACF011)"                         /* @A4 */
             END                                              /* @A4 */
        WHEN (W1 = "SETDIBMP") & (W3 <> "") THEN              /* @A4 */
             IF (SYSDSN("'"W3"'") <> "OK") THEN DO            /* @A4 */
                RACFSMSG = "Invalid RACF Panel DSN"           /* @A4 */
                RACFLMSG = "The PANEL dataset defined",       /* @A4 */
                           "to variable SETDIBMP does",       /* @A4 */
                           "not exist, DSN="W3".",            /* @A4 */
                           "Please update/fix the site",      /* @A4 */
                           "default settings in panel",       /* @A4 */
                           "member $DEFSETG, delete member",  /* @A4 */
                           "RADMPROF in your ISPF profile",   /* @A4 */
                           "dataset and re-invoke RACFADM."   /* @A4 */
                "SETMSG MSG(RACF011)"                         /* @A4 */
             END                                              /* @A4 */
        WHEN (W1 = "SETDIBMS") & (W3 <> "") THEN              /* @A4 */
             IF (SYSDSN("'"W3"'") <> "OK") THEN DO            /* @A4 */
                RACFSMSG = "Invalid RACF Skeleton DSN"        /* @A4 */
                RACFLMSG = "The SKELETON dataset defined",    /* @A4 */
                           "to variable SETDIBMS does",       /* @A4 */
                           "not exist, DSN="W3".",            /* @A4 */
                           "Please update/fix the site",      /* @A4 */
                           "default settings in panel",       /* @A4 */
                           "member $DEFSETG, delete member",  /* @A4 */
                           "RADMPROF in your ISPF profile",   /* @A4 */
                           "dataset and re-invoke RACFADM."   /* @A4 */
                "SETMSG MSG(RACF011)"                         /* @A4 */
             END                                              /* @A4 */
        WHEN (W1 = "SETDIBMC") & (W3 <> "") THEN              /* @A4 */
             IF (SYSDSN("'"W3"'") <> "OK") THEN DO            /* @A4 */
                RACFSMSG = "Invalid RACF CLIST DSN"           /* @A4 */
                RACFLMSG = "The CLIST dataset defined",       /* @A4 */
                           "to variable SETDIBMC does",       /* @A4 */
                           "not exist, DSN="W3".",            /* @A4 */
                           "Please update/fix the site",      /* @A4 */
                           "default settings in panel",       /* @A4 */
                           "member $DEFSETG, delete member",  /* @A4 */
                           "RADMPROF in your ISPF profile",   /* @A4 */
                           "dataset and re-invoke RACFADM."   /* @A4 */
                "SETMSG MSG(RACF011)"                         /* @A4 */
             END                                              /* @A4 */
        OTHERWISE NOP
     END

     INTERPRET W1" = W3L"                                     /* @A7 */
     INTERPRET "'VPUT ("W1") PROFILE'"
  END

  IF (SETDIBMM = "") & (SETDIBMP = ""),                       /* @A4 */
   & (SETDIBMS = "") & (SETDIBMC = "") THEN                   /* @A4 */
     NOP                                                      /* @A4 */
  ELSE DO                                                     /* @A4 */
     IF (SETDIBMM <> "") & (SETDIBMP <> ""),                  /* @A4 */
      & (SETDIBMS <> "") & (SETDIBMC <> "") THEN              /* @A4 */
         NOP                                                  /* @A4 */
     ELSE DO                                                  /* @A4 */
         RACFSMSG = "Invalid - RACF DSNs"                     /* @A4 */
         RACFLMSG = "When defining IBM RACF datasets in",     /* @A4 */
                    "the $DEFSETG member, they must contain", /* @A4 */
                    "dataset names in ALL the variables",     /* @A4 */
                    "(SETDIBMM/P/S/C) or the variables must", /* @A4 */
                    "ALL be null/empty.  Please update/fix",  /* @A4 */
                    "$DEFSETG in the panel dataset, delete",  /* @A4 */
                    "RADMPROF member in your ISPF profile",   /* @A4 */
                    "dataset and re-invoke RACFADM."          /* @A4 */
         "SETMSG MSG(RACF011)"                                /* @A4 */
     END                                                      /* @A4 */
  END                                                         /* @A4 */
  DROP REC.                                                   /* @A6 */
RETURN
