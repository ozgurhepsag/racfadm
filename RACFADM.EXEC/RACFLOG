/*%NOCOMMENT====================* REXX *==============================*/
/*  PURPOSE:  RACFADM - Changes/Issues/ISPLog - Menu Options C, I, L  */
/*--------------------------------------------------------------------*/
/* FLG  YYMMDD  USERID   DESCRIPTION                                  */
/* ---  ------  -------  -------------------------------------------- */
/* @AI  200514  TRIDJK   Support ISPLOG SYSOUT datasets - SDSF/(E)JES */
/* @AH  200506  RACFA    Display headers in a different color         */
/* @AG  200504  RACFA    Make long msg more meaningful                */
/* @AF  200504  RACFA    Added the capability to view ISPLOG file     */
/* @AE  200423  RACFA    Move PARSE REXXPGM name up above IF SETMTRAC */
/* @AD  200423  RACFA    Ensure REXX program name is 8 chars long     */
/* @AC  200422  RACFA    Fixed displaying error message               */
/* @AB  200413  RACFA    Chg TRACEing to only display banner (P=Pgms) */
/* @AA  200412  RACFA    Chg TRACE to allow 'L'abels or 'R'esults     */
/* @A9  200402  RACFA    Issue message if no LIBDEF ISPPLIB dataset   */
/* @A8  200401  RACFA    Chged edit macro RACFLOGE to RACFEMAC        */
/* @A7  200303  RACFA    Def/use variable for edit macro              */
/* @A6  200303  RACFA    Renamed edit macro to RACFDSPE, was RACFCHGE */
/* @A5  200303  RACFA    Receive member as a parmater                 */
/* @A4  200303  RACFA    Renamed mbr to RACFDSP, was RACFCHGS         */
/* @A3  200221  RACFA    Make 'ADDRESS ISPEXEC' defualt, reduce code  */
/* @A2  200220  RACFA    Added SETMTRAC=YES, then TRACE R             */
/* @A1  200220  RACFA    Added capability to browse/edit/view file    */
/* @A0  200218  RACFA    Created REXX                                 */
/*====================================================================*/
PANEL01     = "RACFRPTS"   /* Display with colors          */ /* @AH */
EDITMACR    = "RACFEMAC"   /* Edit Macro, turn HILITE off  */ /* @A8 */
LAST        = "LAST"       /* EDITMACR PARM for ISPLOG     */ /* @AI */
parse source . . REXXPGM .                                    /* @AE */
REXXPGM     = LEFT(REXXPGM,8)                                 /* @AE */

ADDRESS ISPEXEC                                               /* @A3 */
  PARSE ARG MEMBER                                            /* @A5 */
  "VGET (SETGDISP SETMTRAC) PROFILE"                          /* @A2 */
  If (SETMTRAC <> 'NO') then do                               /* @AA */
     Say "*"COPIES("-",70)"*"                                 /* @AA */
     Say "*"Center("Begin Program = "REXXPGM,70)"*"           /* @AA */
     Say "*"COPIES("-",70)"*"                                 /* @AA */
     if (SETMTRAC <> 'PROGRAMS') THEN                         /* @AB */
        interpret "Trace "SUBSTR(SETMTRAC,1,1)                /* @AA */
  end                                                         /* @AA */
  IF (member = "ISPLOG") THEN                                 /* @AF */
     CALL ISPLOG_CHECK                                        /* @AI */
  ELSE                                                        /* @AF */
     CALL LOG_FILE
  CALL GOODBYE                                                /* @AI */

ISPLOG_CHECK:                                                 /* @AI */
  "VGET (ZLOGNAME)"                                           /* @AI */
  If right(zlogname,2) = '.?' then       /* SYSOUT? */        /* @AI */
    Call ISPLOG_SYSOUT                                        /* @AI */
  else                                                        /* @AI */
    Call ISPLOG_FILE                                          /* @AI */
RETURN                                                        /* @AI */

GOODBYE:
  If (SETMTRAC <> 'NO') then do                               /* @AA */
     Say "*"COPIES("-",70)"*"                                 /* @AA */
     Say "*"Center("End Program = "REXXPGM,70)"*"             /* @AA */
     Say "*"COPIES("-",70)"*"                                 /* @AA */
  end                                                         /* @AA */
EXIT                                                          /* @AA */
/*--------------------------------------------------------------------*/
/*  Display the Change or Issues Log file                             */
/*--------------------------------------------------------------------*/
LOG_FILE:
  "QLIBDEF ISPPLIB TYPE(DATASET) ID(DSNAME)"
  if (RC > 0) then do                                         /* @A9 */
     call RACFMSGS ERR19                                      /* @AC */
     return                                                   /* @AA */
  end                                                         /* @A9 */
  DSNAME = "'"STRIP(DSNAME,,"'")"("MEMBER")'"                 /* @A5 */

  SELECT                                                      /* @A1 */
     WHEN (SETGDISP = "VIEW") THEN                            /* @A1 */
          "VIEW DATASET("DSNAME") MACRO("EDITMACR")",         /* @AH */
               "PANEL("PANEL01")"                             /* @AH */
     WHEN (SETGDISP = "EDIT") THEN                            /* @A1 */
          "EDIT DATASET("DSNAME") MACRO("EDITMACR")",         /* @AH */
               "PANEL("PANEL01")"                             /* @AH */
     OTHERWISE                                                /* @A1 */
          "BROWSE DATASET("DSNAME")"                          /* @A1 */
  END                                                         /* @A1 */
RETURN                                                        /* @AA */
/*--------------------------------------------------------------------*/
/*  Display the ISPLog file                                      @AF  */
/*--------------------------------------------------------------------*/
ISPLOG_FILE:                                                  /* @AF */
  "VGET (ZLOGNAME)"                                           /* @AF */
  IF (ZLOGNAME = "") THEN DO                                  /* @AF */
     RACFSMSG = ""                                            /* @AF */
     RACFLMSG = "The ISPF Log file is not allocated or",      /* @AF */
                "may not have been written to.  Verify",      /* @AF */
                "ISPF Settings (=0), 'Log/List',",            /* @AG */
                "'1. Log Data set defaults', has primary",    /* @AG */
                "and secondary pages.  If not, suggest",      /* @AG */
                "making 'Primary pages = 10' and",            /* @AG */
                "'Secondary pages = 10', then",               /* @AG */
                "terminate and re-invoke ISPF."               /* @AG */
     'setmsg msg(RACF011)'                                    /* @AF */
     return                                                   /* @AF */
  END                                                         /* @AF */
                                                              /* @AF */
  "SELECT PGM(ISPLLP) PARM(LOG KEEP) SUSPEND"                 /* @AF */
                                                              /* @AF */
  ADDRESS TSO "ALLOC FI("DDNAME") DA('"ZLOGNAME"') SHR REUS"  /* @AF */
  IF (RC > 0) THEN DO                                         /* @AF */
     RACFSMSG = ""                                            /* @AF */
     RACFLMSG = "Unable to allocate the ISPF log file:",      /* @AF */
                 ZLOGNAME".  Please investigate."             /* @AF */
     'setmsg msg(RACF011)'                                    /* @AF */
     return                                                   /* @AF */
  END                                                         /* @AF */
                                                              /* @XX */
  ADDRESS TSO "EXECIO * DISKR "DDNAME" (STEM RECIN. FINIS"    /* @XX */
  ADDRESS TSO "FREE FI("DDNAME")"                             /* @XX */
                                                              /* @XX */
  K = 0                                                       /* @XX */
  DO J = RECIN.0 To 1 by -1                                   /* @XX */
     K = K + 1                                                /* @XX */
     RECOUT.K = RECIN.J                                       /* @XX */
  END                                                         /* @XX */
  RECOUT.0 = K                                                /* @XX */
  DROP RECIN.
                                                              /* @XX */
  ADDRESS TSO "ALLOC F("DDNAME") NEW REUSE",                  /* @XX */
              "LRECL(125) BLKSIZE(0) RECFM(V B A)",           /* @XX */
              "UNIT(VIO) SPACE(1 5) CYLINDERS"                /* @XX */
  ADDRESS TSO "EXECIO * DISKW "DDNAME" (STEM RECOUT. FINIS"   /* @XX */
  DROP RECOUT.                                                /* @XX */
                                                              /* @XX */
  "LMINIT DATAID(DATAID) DDNAME("DDNAME")"                    /* @XX */
  SELECT                                                      /* @XX */
     WHEN (SETGDISP = "VIEW") THEN                            /* @XX */
          "VIEW DATAID("DATAID") MACRO("EDITMACR")",          /* @XX */
               "PANEL("PANEL01")"                             /* @AH */
     WHEN (SETGDISP = "EDIT") THEN                            /* @XX */
          "EDIT DATAID("DATAID") MACRO("EDITMACR")",          /* @XX */
               "PANEL("PANEL01")"                             /* @AH */
     OTHERWISE                                                /* @XX */
          "BROWSE DATAID("DATAID")"                           /* @XX */
  END                                                         /* @XX */
  'LMFREE DATAID('DATAID')'                                   /* @AI */
  ADDRESS TSO "FREE FI("DDNAME")"                             /* @XX */
                                                              /* @AF */
RETURN                                                        /* @AF */

ISPLOG_SYSOUT:                                                /* @AI */
  ADDRESS ISPEXEC
  racflmsg = "Retrieving data - Please be patient"
  "control display lock"
  "display msg(RACF011)"

  Call DO_ISFCALLS

  Address TSO
  'alloc f('DDNAME')  new reuse unit(sysallda)',
         'space(20,50) cylinders',
         'recfm(V B A) lrecl(125) blksize(0)'
  'execio * diskw 'DDNAME' (stem racfadm. finis'
  drop racfadm.

  Address ISPExec
  /*---------------------*/
  /*  Browse the report  */
  /*---------------------*/
  'lminit dataid(id) ddname('DDNAME')'
  if (rc ^= 0) then do
     racfsmsg = 'Allocation error'
     racflmsg = 'LMINIT failed for 'DDNAME
     'setmsg msg(racf011)'
     RETURN
  end
  SELECT
     WHEN (SETGDISP = "VIEW") THEN
          "VIEW DATAID("ID") MACRO("EDITMACR") PARM("LAST")"
     WHEN (SETGDISP = "EDIT") THEN
          "EDIT DATAID("ID") MACRO("EDITMACR") PARM("LAST")"
     OTHERWISE
          "BROWSE DATAID("ID")"
  END
  'lmfree dataid('id')'
  Address TSO 'Free fi('DDNAME')'

RETURN
/*--------------------------------------------------------------------*/
/*  Check for ISPLOG SYSOUT                                           */
/*--------------------------------------------------------------------*/
DO_ISFCALLS:                                                  /* @AI */
   rc=isfcalls('ON')
   /*-----------------------------*/
   /*  Access the STATUS display  */
   /*-----------------------------*/
   Address SDSF "ISFEXEC ST "userid()
   /*-------------------*/
   /*  Find TSO Userid  */
   /*-------------------*/
   do ix=1 to JNAME.0
      if JNAME.ix = userid() & ,
        QUEUE.ix = "EXECUTION" & ,
        ACTSYS.ix <> "" then
      do
         /*-----------------------------------------*/
         /*  Issue the ? (JDS) action against the   */
         /*  row to list the data sets in the job.  */
         /*-----------------------------------------*/
         Address SDSF "ISFACT ST TOKEN('"TOKEN.ix"') PARM(NP ?)" ,
           "( prefix jds_"
         /*---------------------------------------------*/
         /*  Find the ISPLOG data sets and read them    */
         /*  using ISFBROWSE.  Use isflinelim to limit  */
         /*  the number of REXX variables returned.     */
         /*---------------------------------------------*/
         isflinelim=5000
         ky = 1
         racfadm. = ''
         do jx=1 to jds_DDNAME.0
            if left(jds_DDNAME.jx,6) = "ISPLOG" then
            do
               /*---------------------------------------*/
               /*  Read the records from the data set.  */
               /*---------------------------------------*/
               do until isfnextlinetoken=''
                  Address SDSF "ISFBROWSE ST TOKEN('"jds_TOKEN.jx"')"
                  do kx=1 to isfline.0
                     racfadm.ky = isfline.kx
                     ky = ky + 1
                     end
                  /*-----------------------------*/
                  /*  Set start for next browse  */
                  /*-----------------------------*/
                  isfstartlinetoken = isfnextlinetoken
               end
            end
         end
      end
   end
   rc=isfcalls('OFF')

RETURN
