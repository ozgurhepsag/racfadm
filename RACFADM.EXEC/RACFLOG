/*%NOCOMMENT====================* REXX *==============================*/
/*  PURPOSE:  RACFADM - Changes/Issues - Menu option C and I          */
/*--------------------------------------------------------------------*/
/* FLG  YYMMDD  USERID   DESCRIPTION                                  */
/* ---  ------  -------  -------------------------------------------- */
/* @AD  200423  RACFA    Ensure REXX program name is 8 chars long     */
/* @AC  200422  RACFA    Fixed displaying error message               */
/* @AB  200413  RACFA    Chg TRACEing to only display banner (P=Pgms) */
/* @AA  200412  RACFA    Chg TRACE to allow 'L'abels or 'R'esults     */
/* @A9  200402  RACFA    Issue message if no LIBDEF ISPPLIB dataset   */
/* @A8  200401  RACFA    Chged edit macro RACFLOGE to RACFEMAC        */
/* @A7  200303  RACFA    Def/use variable for edit macro              */
/* @A6  200303  RACFA    Renamed edit macro to RACFDSPE, was RACFCHGE */
/* @A5  200303  RACFA    Receive member as a parmater                 */
/* @A4  200303  RACFA    Renamed mbr to RACFDSP, was RACFCHGS         */
/* @A3  200221  RACFA    Make 'ADDRESS ISPEXEC' defualt, reduce code  */
/* @A2  200220  RACFA    Added ERATRACE=YES, then TRACE R             */
/* @A1  200220  RACFA    Added capability to browse/edit/view file    */
/* @A0  200218  RACFA    Created REXX                                 */
/*====================================================================*/
EDITMACR = "RACFEMAC"    /* Edit Macro, turn HILITE off    */ /* @A8 */

ADDRESS ISPEXEC                                               /* @A3 */
  PARSE ARG MEMBER                                            /* @A5 */
  "VGET (ERADISP ERATRACE) ASIS"                              /* @A2 */
  If (eratrace <> 'NO') then do                               /* @AA */
     parse source . . REXXPGM .                               /* @AA */
     REXXPGM = LEFT(REXXPGM,8)                                /* @AD */
     Say "*"COPIES("-",70)"*"                                 /* @AA */
     Say "*"Center("Begin Program = "REXXPGM,70)"*"           /* @AA */
     Say "*"COPIES("-",70)"*"                                 /* @AA */
     if (eratrace <> 'PROGRAMS') THEN                         /* @AB */
        interpret "Trace "SUBSTR(ERATRACE,1,1)                /* @AA */
  end                                                         /* @AA */

  "QLIBDEF ISPPLIB TYPE(DATASET) ID(DSNAME)"
  if (RC > 0) then do                                         /* @A9 */
     call RACFMSGS ERR19                                      /* @AC */
     call Goodbye                                             /* @AA */
  end                                                         /* @A9 */
  DSNAME = "'"STRIP(DSNAME,,"'")"("MEMBER")'"                 /* @A5 */

  SELECT                                                      /* @A1 */
     WHEN (ERADISP = "VIEW") THEN                             /* @A1 */
          "VIEW DATASET("DSNAME") MACRO("EDITMACR")"          /* @A7 */
     WHEN (ERADISP = "EDIT") THEN                             /* @A1 */
          "EDIT DATASET("DSNAME") MACRO("EDITMACR")"          /* @A7 */
     OTHERWISE                                                /* @A1 */
          "BROWSE DATASET("DSNAME")"                          /* @A1 */
  END                                                         /* @A1 */
  call Goodbye                                                /* @AA */
EXIT                                                          /* @AA */
/*--------------------------------------------------------------------*/
/*  If tracing is on, display flower box                         @AA  */
/*--------------------------------------------------------------------*/
GOODBYE:                                                      /* @AA */
  If (eratrace <> 'NO') then do                               /* @AA */
     Say "*"COPIES("-",70)"*"                                 /* @AA */
     Say "*"Center("End Program = "REXXPGM,70)"*"             /* @AA */
     Say "*"COPIES("-",70)"*"                                 /* @AA */
  end                                                         /* @AA */
EXIT                                                          /* @AA */
