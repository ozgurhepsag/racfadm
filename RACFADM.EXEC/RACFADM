/*%NOCOMMENT====================* REXX *==============================*/
/*  PURPOSE:  RACFADM - Display Menu - LIBDEF and ALTLIB datasets     */
/*--------------------------------------------------------------------*/
/* FLG  YYMMDD  USERID   DESCRIPTION                                  */
/* ---  ------  -------  -------------------------------------------- */
/* @A1  200120  LBD      Added code for sites using qual. ISPPÝM¨LIB  */
/* @A0  200120  LBD      Created REXX                                 */
/*====================================================================*/
Address ISPExec
  call Setup
  'Select Panel(racfmenu)'
  'libdef ispmlib'
  'libdef ispplib'

Address TSO 'altlib deact app(exec)'
exit 0
/*--------------------------------------------------------------------*/
/*  Obtain dataset names and LIBDEF/ALTLIB them                       */
/*--------------------------------------------------------------------*/
SETUP:
  parse source src
  parse value src with TSO Type Name DDName DSName NameO Env Addr Token
  cmd = name
  if DSName /= '?' then exec =  "'"dsname"'"
  else do
     x = listdsi(DDName 'FILE')
     exec = "'"sysdsname"'"
     dsname = sysdsname
  end
  dsname = translate(dsname,' ','.')
  panels = "'"subword(dsname,1,words(dsname)-1)" PANELS'"
  msgs   = "'"subword(dsname,1,words(dsname)-1)" MSGS'"
  plib   = "'"subword(dsname,1,words(dsname)-1)" ISPPLIB'"    /* @A1 */
  mlib   = "'"subword(dsname,1,words(dsname)-1)" ISPMLIB'"    /* @A1 */
  panels = translate(panels,'.',' ')
  msgs   = translate(msgs,'.',' ')
  plib   = translate(plib,'.',' ')                            /* @A1 */
  mlib   = translate(mlib,'.',' ')                            /* @A1 */
  x = listdsi(plib)                                           /* @A1 */
  if x = 0 then panels = plib                                 /* @A1 */
  x = listdsi(mlib)                                           /* @A1 */
  if x = 0 then msgs = mlib                                   /* @A1 */
  Address TSO 'Altlib Act app(Exec) Dataset('exec')'
  'Libdef ISPMLIB dataset id('msgs') Stack'
  'Libdef ISPPLIB dataset id('panels') Stack'
RETURN
