/*%NOCOMMENT====================* REXX *==============================*/
/*  PURPOSE:  RACFADM - General resources - List classes (Menu Opt 4) */
/*--------------------------------------------------------------------*/
/* FLG  YYMMDD  USERID   DESCRIPTION                                  */
/* ---  ------  -------  -------------------------------------------- */
/* @AB  200222  RACFA    Removed translating OPTA/B, not needed       */
/* @AA  200222  RACFA    Allow placing cursor on row and press ENTER  */
/* @A9  200221  RACFA    Added primary commands 'ONLY' and 'RESET'    */
/* @A8  200221  RACFA    Allow abbreviating the 'LOCATE' command      */
/* @A7  200221  RACFA    Make 'ADDRESS ISPEXEC' defualt, reduce code  */
/* @A6  200220  RACFA    Fixed displaying all RACF commands           */
/* @A5  200220  RACFA    Added ERATRACE=YES, then TRACE R             */
/* @A4  200218  RACFA    Condense VGETs into one line                 */
/* @A3  200120  RACFA    Removed 'say msg.msg_var' in EXCMD procedure */
/* @A2  200119  RACFA    Standardized/reduced lines of code           */
/* @A1  200119  RACFA    Placed comment boxes above procedures        */
/* @A0  011229  NICORIZ  Created REXX, V2.1, www.rizzuto.it           */
/*====================================================================*/
ADDRESS ISPEXEC                                               /* @A7 */
  Arg class
  "VGET (ZSCREEN ERASHOW ERATRACE) ASIS"                      /* @A4 */
  If (eratrace = 'YES') then Trace r                          /* @A5 */

  address TSO "PROFILE MSGID"
  rlv = SYSVAR('SYSLRACF')
  if (class = '') then DO                                     /* @A9 */
     call Select_class
     rc = display_table()                                     /* @A9 */
     "TBEND" tbla                                             /* @A9 */
  END                                                         /* @A9 */
  else
     call RACFCLSG class '**' 'YES' /*generic prof routine*/
EXIT
/*--------------------------------------------------------------------*/
/*  Select class                                                      */
/*--------------------------------------------------------------------*/
SELECT_CLASS:
  seconds = time('S')
  tbla= 'TB'ZSCREEN||SECONDS  /* table name unique */
  "TBCREATE" TBLA "KEYS(CLASS)",
                  "NAMES(ACTION) REPLACE NOWRITE"
  call get_act_class
RETURN
/*--------------------------------------------------------------------*/
/*  Display profile permits                                           */
/*--------------------------------------------------------------------*/
GET_ACT_CLASS:
  Scan = 'OFF'
  cmd  = "SETROPTS LIST"                                      /* @A6 */
  if (erashow = 'YES') then                                   /* @A6 */
     call SHOWCMD                                             /* @A6 */
  x = OUTTRAP('var.')
  address TSO cmd                                             /* @A6 */
  x = OUTTRAP('OFF')
  Do i = 1 to var.0         /* Scan output */
     temp = var.i
     if (rlv > '1081') then  /* RACF 1.9 add blank */
        temp= ' 'temp
     Select
        when (substr(temp,2,16) = 'ACTIVE CLASSES =') then do
             scan   = 'ON'
             record = substr(temp,18,80)
             nwords = words(record)
             do t = 1 to nwords
                class = subword(record,t,1)
                if (class <> 'USER'),
                 & (class <> 'DATASET'),
                 & (class <> 'GROUP') then
                   "TBMOD" tbla
             end
        end
        when (substr(temp,2,25) = 'GENERIC',
             'PROFILE CLASSES =') then leave
        otherwise
          if (scan = 'ON') then do
             record = var.i
             nwords = words(record)
             do t = 1 to nwords
                class = subword(record,t,1)
                if (class <> 'USER'),
                 & (class <> 'DATASET'),
                 & (class <> 'GROUP') then
                   "TBMOD" tbla
             end
        end
     End  /* end_select */
  end /* do i */
RETURN
/*--------------------------------------------------------------------*/
/*  Profiles table display section                                    */
/*--------------------------------------------------------------------*/
DISPLAY_TABLE:
  opta = ' '
  "TBSORT " tbla "FIELDS(CLASS)"
  "TBTOP  " tbla
  "TBDISPL" tbla "PANEL(RACFCLSR)"
  reta = rc
  do while (reta < 8)   /* tablea */
     action = ''
     "CONTROL DISPLAY SAVE"
     xtdtop = ztdtop                                          /* @A9 */
     lclass = ''                                              /* @A9 */
     PARSE VAR ZCMD ZCMD PARM                                 /* @A8 */
     IF (SROW <> "") & (SROW <> 0) THEN                       /* @AA */
        IF (SROW > 0) THEN DO                                 /* @AA */
           "TBTOP " TBLA                                      /* @AA */
           "TBSKIP" TBLA "NUMBER("SROW")"                     /* @AA */
        END                                                   /* @AA */
     Select
        When (abbrev("ONLY",zcmd,1) = 1) then do              /* @A9 */
             find_str = translate(parm)                       /* @A9 */
             'tbtop ' tbla                                    /* @A9 */
             'tbskip' tbla                                    /* @A9 */
             do forever                                       /* @A9 */
                str = translate(class action)                 /* @A9 */
                if (pos(find_str,str) > 0) then nop           /* @A9 */
                else 'tbdelete' tbla                          /* @A9 */
                'tbskip' tbla                                 /* @A9 */
                if (rc > 0) then do                           /* @A9 */
                   'tbtop' tbla                               /* @A9 */
                   leave                                      /* @A9 */
                end                                           /* @A9 */
             end                                              /* @A9 */
        END                                                   /* @A9 */
        WHEN (ABBREV("RESET",ZCMD,1) = 1) THEN DO             /* @A9 */
             call select_class                                /* @A9 */
             fld_sort = 'CLASS,C,A'                           /* @A9 */
             xtdtop   = 1                                     /* @A9 */
        END                                                   /* @A9 */
        WHEN (ABBREV("LOCATE",ZCMD,1) = 1) THEN               /* @A8 */
             lclass = parm                                    /* @A8 */
        OTHERWISE NOP                                         /* @A8 */
     End
     Select
        when (opta = 'S') then
             call RACFCLSG class '**' 'YES' /* call generic */
        when (opta = 'R') then call REFRESH
        otherwise nop
     End
     Do while (reta = 4)   /* process multi selection */
        "CONTROL DISPLAY RESTORE"
        "TBDISPL" tbla
        reta = rc
        "CONTROL DISPLAY SAVE"
        Select
           when (opta = 'S') then
                call RACFCLSG class '**' 'YES' /* gen prof. routine */
           when (opta = 'R') then call REFRESH
           otherwise nop
        End
     end
     opta = ' '
     "TBSORT" tbla "FIELDS(CLASS)"
     "TBTOP " tbla
     if (lclass <> '') then do
        class = lclass
        "TBSARG" TBLA "NEXT NAMECOND(CLASS,GE)"
        "TBSCAN" TBLA "NOREAD"
     end
     else
        "TBSKIP" tbla "NUMBER("XTDTOP")"
     "CONTROL DISPLAY RESTORE"
     "TBDISPL" tbla "PANEL(RACFCLSR)"
     reta = rc
  end  /* Do while (reta < 8) */
RETURN 0
/*--------------------------------------------------------------------*/
/*  Refresh class                                                     */
/*--------------------------------------------------------------------*/
REFRESH:
  msg    = 'You are about to refresh class 'class
  Sure_? = RACFMSGC(msg)
  if (sure_? = 'YES') then do
     call EXCMD "SETR RACLIST("class") REFRESH"
     if (rc <> 0) then call RACFMSGS "ERR10" /* CMD FAILED */
     else do
        action = '*REFRESHED'
        "TBMOD" tbla
     end
  end
RETURN
/*--------------------------------------------------------------------*/
/*  Exec command                                                      */
/*--------------------------------------------------------------------*/
EXCMD:
  signal off error
  arg cmd
  if (erashow = 'YES') then                                   /* @A6 */
     call SHOWCMD                                             /* @A6 */
  x = OUTTRAP('msg.')
  address TSO ""cmd""
  ret_code = rc
  x = OUTTRAP('OFF')
  if (subword(msg.1,1,1)= 'ICH11009I') |,
     (subword(msg.1,1,1)= 'ICH10006I') |,
     (subword(msg.1,1,1)= 'ICH06011I') then
     raclist = 'YES'
  else
     msg_var = 1 to msg.0                                     /* @A3 */
RETURN
/*--------------------------------------------------------------------*/
/*  Display RACF command                                          @A6 */
/*--------------------------------------------------------------------*/
SHOWCMD:                                                      /* @A6 */
  PARSE VAR CMD MSG1 60 MSG2 121 MSG3                         /* @A6 */
  'ADDPOP ROW(6) COLUMN(4)'                                   /* @A6 */
  'DISPLAY PANEL(RACFMSG2)'                                   /* @A6 */
  'REMPOP'                                                    /* @A6 */
RETURN                                                        /* @A6 */
