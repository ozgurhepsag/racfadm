/*%NOCOMMENT====================* REXX *==============================*/
/*  PURPOSE:  RACFADM - Get active classes and call RACFCLSG          */
/*--------------------------------------------------------------------*/
/* FLG  YYMMDD  USERID   DESCRIPTION                                  */
/* ---  ------  -------  -------------------------------------------- */
/* @A3  200120  RACFA    Removed 'say msg.msg_var' in EXCMD procedure */
/* @A2  200119  RACFA    Standardized/reduced lines of code           */
/* @A1  200119  RACFA    Placed comment boxes above procedures        */
/* @A0  011229  NICORIZ  Created REXX, V2.1, www.rizzuto.it           */
/*====================================================================*/
  Trace o
  Arg class
  address ISPEXEC "VGET (ZSCREEN) ASIS"
  address ISPEXEC "VGET (ERASHOW) ASIS"
  address ISPEXEC "VGET (ERATRACE) ASIS"

  address TSO "PROFILE MSGID"
  rlv= SYSVAR('SYSLRACF')
  if class='' then
     call Select_class
  else
     call RACFCLSG class '**' 'YES' /*generic prof routine*/
EXIT
/*--------------------------------------------------------------------*/
/*  Select class                                                      */
/*--------------------------------------------------------------------*/
SELECT_CLASS:
  seconds=time('S')
  tbla= 'TB'ZSCREEN||SECONDS  /* table name unique */
  address ISPEXEC "TBCREATE" TBLA "KEYS(CLASS)",
                  "NAMES(ACTION) REPLACE NOWRITE"
  call get_act_class
  rc=display_table()
  address ISPEXEC "TBEND" tbla
RETURN
/*--------------------------------------------------------------------*/
/*  Display profile permits                                           */
/*--------------------------------------------------------------------*/
Get_act_class:
  Scan='OFF'
  x= OUTTRAP('var.')
  address TSO "SETROPTS LIST"
  x= OUTTRAP('OFF')
  Do i= 1 to var.0         /* Scan output */
     temp= var.i
     if rlv > '1081' then  /* RACF 1.9 add blank */
        temp= ' 'temp
     Select
        when substr(temp,2,16)= 'ACTIVE CLASSES =' then do
             scan='ON'
             record=substr(temp,18,80)
             nwords=words(record)
             do t=1 to nwords
                class=subword(record,t,1)
                if class<>'USER',
                 & class<>'DATASET',
                 & class<>'GROUP' then
                   address ISPEXEC "TBMOD" tbla
             end
        end
        when substr(temp,2,25)='GENERIC',
             'PROFILE CLASSES =' then leave
        otherwise
          if scan='ON' then do
             record=var.i
             nwords=words(record)
             do t=1 to nwords
                class=subword(record,t,1)
                if class<>'USER',
                 & class<>'DATASET',
                 & class<>'GROUP' then
                   address ISPEXEC "TBMOD" tbla
             end
        end
     End  /* end_select */
  end /* do i */
RETURN
/*--------------------------------------------------------------------*/
/*  profiles table display section                                    */
/*--------------------------------------------------------------------*/
Display_table:
  opta= ' '
  address ISPEXEC "TBSORT" tbla "FIELDS(CLASS)"
  address ISPEXEC "TBTOP" tbla
  address ISPEXEC "TBDISPL" tbla "PANEL(RACFCLSR)"
  reta= rc
  do while (reta < 8)   /* tablea */
     action=''
     address ISPEXEC "CONTROL DISPLAY SAVE"
     Select
        when subword(ZCMD,1,1)= 'LOCATE',
           | subword(ZCMD,1,1)= 'L' then
             lclass=subword(ZCMD,2,1)
        otherwise lclass=''
     End
     xtdtop= ztdtop
     opta= translate(opta) /* upper case */
     Select
        when opta= 'S' then
             call RACFCLSG class '**' 'YES' /* call generic */
        when opta= 'R' then call REFRESH
        otherwise nop
     End
     Do while (reta = 4)   /* process multi selection */
        address ISPEXEC "CONTROL DISPLAY RESTORE"
        address ISPEXEC "TBDISPL" tbla
        reta= rc
        address ISPEXEC "CONTROL DISPLAY SAVE"
        opta= translate(opta) /* upper case */
        Select
           when opta= 'S' then
                call RACFCLSG class '**' 'YES' /* gen prof. routine */
           when opta= 'R' then call REFRESH
           otherwise nop
        End
     end
     opta= ' '
     address ISPEXEC "TBSORT" tbla "FIELDS(CLASS)"
     address ISPEXEC "TBTOP" tbla
     if lclass<>'' then do
        class=lclass
        ADDRESS ISPEXEC "TBSARG" TBLA "NEXT NAMECOND(CLASS,GE)"
        ADDRESS ISPEXEC "TBSCAN" TBLA "NOREAD"
     end
     else address ISPEXEC "TBSKIP" tbla "NUMBER("XTDTOP")"
     address ISPEXEC "CONTROL DISPLAY RESTORE"
     address ISPEXEC "TBDISPL" tbla "PANEL(RACFCLSR)"
     reta= rc
  end  /* Do while (reta < 8) */
RETURN 0
/*--------------------------------------------------------------------*/
/*  Refresh class                                                     */
/*--------------------------------------------------------------------*/
REFRESH:
  msg='You are about to refresh class 'class
  Sure_?= RACFMSGC(msg)
  if sure_? = 'YES' then do
     call EXCMD "SETR RACLIST("class") REFRESH"
     if rc<> 0 then call RACFMSGS "ERR10" /* CMD FAILED */
     else do
        action='*REFRESHED'
        address ISPEXEC "TBMOD" tbla
     end
  end
RETURN
/*--------------------------------------------------------------------*/
/*  Exec Command                                                      */
/*--------------------------------------------------------------------*/
EXCMD:
  signal off error
  arg cmd
  if erashow= 'YES' then do
     address ISPEXEC
     'CONTROL NOCMD'
     'ADDPOP'
     'DISPLAY PANEL(RACFMSG2)'
     'REMPOP'
  end
  x= OUTTRAP('msg.')
  address TSO ""cmd""
  ret_code=rc
  x= OUTTRAP('OFF')
  if subword(msg.1,1,1)= 'ICH11009I' |,
     subword(msg.1,1,1)= 'ICH10006I' |,
     subword(msg.1,1,1)= 'ICH06011I' then raclist= 'YES'
  else
     msg_var= 1 to msg.0                                      /* @A3 */
RETURN
