/*%NOCOMMENT====================* REXX *==============================*/
/*  PURPOSE:  RACFADM - Password Reset - Option P, User line cmd PW   */
/*--------------------------------------------------------------------*/
/* FLG  YYMMDD  USERID   DESCRIPTION                                  */
/* ---  ------  -------  -------------------------------------------- */
/* @A4  200412  RACFA    Chg TRACE to allow 'L'abels or 'R'esults     */
/* @A3  200407  RACFA    Ensure REMPOP is accomplished when F3 (END)  */
/* @A2  200407  RACFA    Made panel a variable                        */
/* @A1  200407  RACFA    Removed unnecessary ISPF variables           */
/* @A0  200407  TRIDJK   Created                                      */
/*====================================================================*/
PANELPW  = "RACFPSWD"    /* Password reset                 */
PANELM2  = "RACFMSG2"    /* Display RACF command and RC    */

parse arg userid
Address ISPExec
  "VGET (ERASHOW ERATRACE ERAADMN) ASIS"                      /* @A1 */
  If (eratrace <> 'NO') then do                               /* @A4 */
     parse source . . REXXPGM .                               /* @A4 */
     Say "*"COPIES("-",70)"*"                                 /* @A4 */
     Say "*"Center("Begin Program = "REXXPGM,70)"*"           /* @A4 */
     Say "*"COPIES("-",70)"*"                                 /* @A4 */
     interpret "Trace "SUBSTR(ERATRACE,1,1)                   /* @A4 */
  end                                                         /* @A4 */
  pswd = newpswd()
  'ADDPOP'
  'DISPLAY PANEL('panelpw')'                                  /* @A2 */
  disprc = RC                                                 /* @A3 */
  'REMPOP'
  if (disprc = 8) then do                                     /* @A3 */
     cmd_rc = 8                                               /* @A4 */
     call Goodbye                                             /* @A4 */
  end                                                         /* @A4 */
  added = ''
  if (resume = 'Y') then
     added = 'RESUME'
  call EXCMD 'ALTUSER 'userid' PASSWORD('pswd')' added
  if (cmd_rc > 0) then do
     racfsmsg = 'ALTUSER failed'
     racflmsg = msg.1
     "SETMSG MSG(RACF011)"
  end
  else do
     racfsmsg = 'Password reset'
     racflmsg = userid 'password reset to 'pswd
     "SETMSG MSG(RACF011)"
  end
  call Goodbye                                                /* @A4 */
EXIT cmd_rc                                                   /* @A3 */
/*--------------------------------------------------------------------*/
/*  If tracing is on, display flower box                         @A4  */
/*--------------------------------------------------------------------*/
GOODBYE:                                                      /* @A4 */
  If (eratrace <> 'NO') then do                               /* @A4 */
     Say "*"COPIES("-",70)"*"                                 /* @A4 */
     Say "*"Center("End Program = "REXXPGM,70)"*"             /* @A4 */
     Say "*"COPIES("-",70)"*"                                 /* @A4 */
  end                                                         /* @A4 */
EXIT cmd_rc                                                   /* @A4 */
/*--------------------------------------------------------------------*/
/*  Generate password                                                 */
/*--------------------------------------------------------------------*/
NEWPSWD:
  /* No vowels, or "V" or "Z" */
  choices  = 'BCDFGHJKLMNPQRSTWXY'
  chars.   = ''
  password = ''
  /* Initialize stem variables */
  do n = 1 to length(choices)
     chars.n = substr(choices,n,1)
  end
  /* Six character password */
  do forever
     pick = random(1,length(choices))
     /* No repeating characters */
     if (pos(chars.pick,password) > 0) then
        nop
     else
        password = password||chars.pick
     if (length(password) > 5) then
        leave
  end
  /* Plug in 1 numeric character */
  number   = random(1,9)
  place    = random(2,6)
  password = overlay(number,password,place,1)
RETURN password
/*--------------------------------------------------------------------*/
/*  Exec command                                                      */
/*--------------------------------------------------------------------*/
EXCMD:
  signal off error
  parse arg cmd
  x = OUTTRAP('msg.')
  address TSO cmd
  cmd_rc = rc
  x = OUTTRAP('OFF')
  if (erashow <> 'NO') then
     call SHOWCMD
  if (subword(msg.1,1,1)= 'ICH11009I') |,
     (subword(msg.1,1,1)= 'ICH10006I') |,
     (subword(msg.1,1,1)= 'ICH06011I') then raclist = 'YES'
RETURN
/*--------------------------------------------------------------------*/
/*  Display RACF command and return code                              */
/*--------------------------------------------------------------------*/
SHOWCMD:
  IF (ERASHOW = "BOTH") | (ERASHOW = "DISPLAY") THEN DO
     PARSE VAR CMD MSG1 60 MSG2 121 MSG3
     MSG4 = "Return code = "cmd_rc
     "ADDPOP ROW(6) COLUMN(4)"
     "DISPLAY PANEL("PANELM2")"
     "REMPOP"
  END
  IF (ERASHOW = "BOTH") | (ERASHOW = "LOG") THEN DO
     zerrsm = "RACFADM RACFPROF RC="WORD(cmd_rc,1)
     zerrlm = cmd
     'log msg(isrz003)'
  END
RETURN
