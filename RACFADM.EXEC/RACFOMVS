/*%NOCOMMENT====================* REXX *==============================*/
/*  PURPOSE:  RACFADM - OMVSCmd - OMVS RACF Commands (Menu option O)  */
/*--------------------------------------------------------------------*/
/*  NOTES:    1) Allows executing the IBM REXX programs:              */
/*                 OPERMIT ... Unix Access Control List, like PERMIT  */
/*                 ORALTER ... Unix Security Attribute, like ALTER    */
/*                 ORLIST .... Unix Dir/File Security, like RLIST     */
/*            2) And one internal command                             */
/*                 FLIST...... Unix ZFS/HFS File Usage/Attribute Info */
/*                 ULIST...... Unix User/Group Identifier (UID/GID)   */
/*--------------------------------------------------------------------*/
/* FLG  YYMMDD  USERID   DESCRIPTION                                  */
/* ---  ------  -------  -------------------------------------------- */
/* @A6  200606  RACFA    Clean up code, reduce no. of subroutines     */
/* @A5  200606  RACFA    Enhanced ULIST to allow passing more parms   */
/* @A4  200606  RACFA    Enhanced FLIST to resolve/display path       */
/* @A3  200606  RACFA    Display report headers in color              */
/* @A2  200606  RACFA    Added FLIST command                          */
/* @A1  200606  RACFA    Added ULIST command                          */
/* @A0  200605  TRIDJK   Created REXX                                 */
/*====================================================================*/
PANEL01     = "RACFOMVS"    /* Obtain cmd, path and options*/
PANEL02     = "RACFRPTS"    /* Display report with colors  */ /* @A3 */
EDITMACR    = "RACFEMAC"    /* Edit Macro, turn HILITE off */
parse source . . REXXPGM .         /* Obtain REXX pgm name */
REXXPGM     = LEFT(REXXPGM,8)

ADDRESS ISPEXEC
  PARSE ARG MEMBER
  "VGET (SETGDISP SETMTRAC) PROFILE"
  If (SETMTRAC <> 'NO') then do
     Say "*"COPIES("-",70)"*"
     Say "*"Center("Begin Program = "REXXPGM,70)"*"
     Say "*"COPIES("-",70)"*"
     if (SETMTRAC <> 'PROGRAMS') THEN
        interpret "Trace "SUBSTR(SETMTRAC,1,1)
  end

  Do Forever
     "DISPLAY PANEL("PANEL01")" /* get utility cmd options*/
     If (rc = 8) then exit
     SELECT                                                   /* @A1 */
        WHEN (abbrev("FLIST",OMVSCMD,1) = 1) then             /* @A5 */
             call flist                                       /* @A2 */
        WHEN (abbrev("ULIST",OMVSCMD,1) = 1) then             /* @A5 */
             call ulist                                       /* @A1 */
        WHEN (abbrev("PERMIT",OMVSCMD,1) = 1) then do         /* @A5 */
             X = OUTTRAP("CMDREC.")                           /* @A1 */
             Address TSO 'O'OMVSCMD OMVSPATH OMVSOPTS
             X = OUTTRAP("OFF")                               /* @A1 */
        end
        OTHERWISE DO                                          /* @A1 */
             X = OUTTRAP("CMDREC.")                           /* @A1 */
             parse var OMVSOPTS w1 w2
             if (w1 = "FSSEC") | (w1 = "fssec") then
                Address TSO 'O'OMVSCMD w1 OMVSPATH w2
             else
                Address TSO 'O'OMVSCMD OMVSPATH OMVSOPTS
             X = OUTTRAP("OFF")                               /* @A1 */
        END                                                   /* @A1 */
     end

     ADDRESS TSO "ALLOC F("DDNAME") NEW REUSE",
                 "LRECL(132) BLKSIZE(0) RECFM(F B)",
                 "UNIT(VIO) SPACE(1 5) CYLINDERS"
     ADDRESS TSO "EXECIO * DISKW "DDNAME,
                 "(STEM CMDREC. FINIS"
     DROP CMDREC.

     "LMINIT DATAID(CMDDATID) DDNAME("DDNAME")"
     SELECT
        WHEN (SETGDISP = "VIEW") THEN DO                      /* @A3 */
             IF (OMVSCMD = "FLIST"),                          /* @A3 */
              | (OMVSCMD = "ULIST") THEN                      /* @A3 */
                "VIEW DATAID("CMDDATID")",                    /* @A3 */
                             "MACRO("EDITMACR")",             /* @A3 */
                             "PANEL("PANEL02")"               /* @A3 */
             ELSE                                             /* @A3 */
                "VIEW DATAID("CMDDATID")",                    /* @A3 */
                             "MACRO("EDITMACR")"              /* @A3 */
        END                                                   /* @A3 */
        WHEN (SETGDISP = "EDIT") THEN DO                      /* @A3 */
             IF (OMVSCMD = "FLIST"),                          /* @A3 */
              | (OMVSCMD = "ULIST") THEN                      /* @A3 */
                "EDIT DATAID("CMDDATID")",                    /* @A3 */
                             "MACRO("EDITMACR")",             /* @A3 */
                             "PANEL("PANEL02")"               /* @A3 */
             ELSE                                             /* @A3 */
                "EDIT DATAID("CMDDATID")",                    /* @A3 */
                             "MACRO("EDITMACR")"              /* @A3 */
        END                                                   /* @A3 */
        OTHERWISE
             "BROWSE DATAID("CMDDATID")"
     END
     ADDRESS TSO "FREE FI("DDNAME")"
  End

  If (SETMTRAC <> 'NO') then do
     Say "*"COPIES("-",70)"*"
     Say "*"Center("End Program = "REXXPGM,70)"*"
     Say "*"COPIES("-",70)"*"
  end
EXIT
/*--------------------------------------------------------------------*/
/*  ULIST - Unix User/Group Identifier (UID/GID)                 @A6  */
/*--------------------------------------------------------------------*/
ULIST:                                                        /* @A1 */
  cmdrec. = ""                                                /* @A1 */
  CHKPRM  = STRIP(OMVSPATH)STRIP(OMVSOPTS)                    /* @A5 */
  PARSE UPPER VAR CHKPRM "UID("UID")" .                       /* @A5 */
  PARSE UPPER VAR CHKPRM "GID("GID")" .                       /* @A5 */
  PARSE VAR CHKPRM "SHELL("SHELL")" .                         /* @A5 */
  if (SHELL = "") THEN                                        /* @A5 */
     PARSE VAR CHKPRM "shell("SHELL")" .                      /* @A5 */
  PARSE VAR CHKPRM "HOME("HOME")" .                           /* @A5 */
  if (HOME = "") THEN                                         /* @A5 */
     PARSE VAR CHKPRM "home("HOME")" .                        /* @A5 */
  PARSE UPPER VAR CHKPRM W1 .                                 /* @A5 */
  IF (POS("(",W1) = 0) THEN                                   /* @A5 */
     ID = W1                                                  /* @A5 */
  ELSE                                                        /* @A5 */
     ID = ""                                                  /* @A5 */
                                                              /* @A5 */
  IF (ID <> "") THEN                                          /* @A5 */
     IF (LENGTH(ID) > 8) THEN DO                              /* @A1 */
        CMDREC.1 = 'The userid ('id') is larger than 8',      /* @A1 */
                   'characters in length.'                    /* @A1 */
        CMDREC.0 = 1                                          /* @A1 */
        return                                                /* @A1 */
     END                                                      /* @A1 */
  IF (UID <> "") THEN                                         /* @A5 */
     IF (LENGTH(UID) > 10) | (DATATYPE(UID) <> "NUM") THEN DO /* @A1 */
        CMDREC.1 = 'The UID ('uid') is larger than 10',       /* @A1 */
                   'characters in length or not numeric.'     /* @A1 */
        CMDREC.0 = 1                                          /* @A1 */
        return                                                /* @A1 */
     END                                                      /* @A1 */
  IF (GID <> "") THEN                                         /* @A5 */
     IF (LENGTH(GID) > 7) | (DATATYPE(GID) <> "NUM") THEN DO  /* @A1 */
        CMDREC.1 = 'The UID ('gid') is larger than 7',        /* @A1 */
                   'characters in length or not numeric.'     /* @A1 */
        CMDREC.0 = 1                                          /* @A1 */
        return                                                /* @A1 */
     END                                                      /* @A1 */
                                                              /* @A1 */
  if (syscalls('ON') > 3) then do                             /* @A1 */
     CMDREC.1 = 'Unable to establish the SYSCALL',            /* @A1 */
                'environment.'                                /* @A1 */
     CMDREC.0 = 1                                             /* @A1 */
     return                                                   /* @A1 */
  end                                                         /* @A1 */
                                                              /* @A1 */
  racflmsg = "Retrieving data - Please be patient"            /* @A1 */
  "control display lock"                                      /* @A1 */
  "display msg(RACF011)"                                      /* @A1 */
                                                              /* @A1 */
  cmdrec.1 = " Userid     UID       GID    Shell   ",         /* @A1 */
             "Home-Directory"                                 /* @A1 */
  cmdrec.2 = "-------- ---------- ------- --------",          /* @A1 */
             "----------------"                               /* @A1 */
  j = 2                                                       /* @A1 */
  address syscall "getpwent pw."                              /* @A1 */
  firstid = pw.1                                              /* @A1 */
  do forever                                                  /* @A1 */
     if (pw.1 <> previd) then do                              /* @A1 */
        IF (CHKPRM = "") THEN do                              /* @A1 */
           J = J + 1                                          /* @A1 */
           cmdrec.J = pw.1 left(pw.2,10) left(pw.3,7),        /* @A1 */
                           left(pw.5,8)  left(pw.4,50)        /* @A1 */
        end                                                   /* @A1 */
        ELSE DO                                               /* @A1 */
           IF ((ID    <> "") & (ID    = PW.1)),               /* @A6 */
            | ((UID   <> "") & (UID   = PW.2)),               /* @A6 */
            | ((GID   <> "") & (GID   = PW.3)),               /* @A6 */
            | ((SHELL <> "") & (SHELL = PW.5)),               /* @A6 */
            | ((HOME  <> "") & (HOME  = PW.4)) THEN DO        /* @A6 */
              J = J + 1                                       /* @A1 */
              cmdrec.J = pw.1 left(pw.2,10) left(pw.3,7),     /* @A1 */
                              left(pw.5,8)  left(pw.4,50)     /* @A1 */
           END                                                /* @A1 */
        end                                                   /* @A1 */
     end                                                      /* @A1 */
     previd = pw.1                                            /* @A1 */
     address syscall "getpwent pw."                           /* @A1 */
     if (retval = -1) then                                    /* @A1 */
        leave                                                 /* @A1 */
     if (pw.1 = firstid) then                                 /* @A1 */
        leave                                                 /* @A1 */
  end                                                         /* @A1 */
  cmdrec.0 = J                                                /* @A1 */
RETURN                                                        /* @A1 */
/*--------------------------------------------------------------------*/
/*  FLIST - Unix ZFS/HFS File Usage/Attribute Information        @A6  */
/*--------------------------------------------------------------------*/
FLIST:                                                        /* @A2 */
  fsm.    = ''                                                /* @A2 */
  fsm.0.0 = ''                                                /* @A2 */
  fsm.0.1 = 'SECACL'                                          /* @A2 */
  fsm.1.0 = ''                                                /* @A2 */
  fsm.1.1 = 'UNMOUNT'                                         /* @A2 */
  fsm.2.0 = ''                                                /* @A2 */
  fsm.2.1 = 'CLIENT'                                          /* @A2 */
  fsm.3.0 = ''                                                /* @A2 */
  fsm.3.1 = 'NOAUTOMOVE'                                      /* @A2 */
  fsm.4.0 = ''                                                /* @A2 */
  fsm.4.1 = 'NOSEC'                                           /* @A2 */
  fsm.5.0 = ''                                                /* @A2 */
  fsm.5.1 = 'DFS-EXPORTED'                                    /* @A2 */
  fsm.6.0 = ''                                                /* @A2 */
  fsm.6.1 = 'NOSETUID'                                        /* @A2 */
  fsm.7.0 = 'RDWR'                                            /* @A2 */
  fsm.7.1 = 'READ'                                            /* @A2 */
  percent = -1                                                /* @A2 */
  string  = ''                                                /* @A2 */
                                                              /* @A2 */
  CHKPRM = STRIP(OMVSPATH)STRIP(OMVSOPTS)                     /* @A5 */
  parse upper var CHKPRM "PERC("P1")" .                       /* @A2 */
  parse upper var CHKPRM "TEXT("P2")" .                       /* @A2 */
  if (p1 = '') & (P2 = '') & (CHKPRM <> '') THEN do           /* @A4 */
     call flist_resolve_path_name                             /* @A4 */
     return                                                   /* @A4 */
  end                                                         /* @A4 */
                                                              /* @A2 */
  p1type = datatype(p1)                                       /* @A2 */
  p2type = datatype(p2)                                       /* @A2 */
  if (p1 = '') then p1type = 'NULL'                           /* @A2 */
  if (p2 = '') then p2type = 'NULL'                           /* @A2 */
                                                              /* @A2 */
  Select                         /* Chk for percentage    */  /* @A2 */
     when (p1type='NULL') then nop                            /* @A2 */
     when (p1type='NUM') then percent=p1                      /* @A2 */
     when (p1type='CHAR') then string=p1                      /* @A2 */
     otherwise do                                             /* @A2 */
          RACFSMSG = "Invalid parameter"                      /* @A2 */
          RACFLMSG = "An invalid parameter was entered,",     /* @A2 */
                     "the syntax is: ZFSUSE Ýpercent¨",       /* @A2 */
                     "Ýstring¨"                               /* @A2 */
          "SETMSG MSG(RACF011)"                               /* @A2 */
          RETURN                                              /* @A2 */
     end                                                      /* @A2 */
  end                                                         /* @A2 */
                                                              /* @A2 */
  Select                         /* Chk for search string */  /* @A2 */
     when (p2type='NULL') then nop                            /* @A2 */
     when (p2type='NUM')  then percent = p2                   /* @A2 */
     when (p2type='CHAR') then string  = p2                   /* @A2 */
     otherwise do                                             /* @A2 */
         RACFSMSG = "Invalid parameter"                       /* @A2 */
         RACFLMSG = "An invalid parameter was entered,",      /* @A2 */
                    "the syntax is: ZFSUSE Ýpercent¨",        /* @A2 */
                    "Ýstring¨"                                /* @A2 */
         "SETMSG MSG(RACF011)"                                /* @A2 */
         RETURN                                               /* @A2 */
     end                                                      /* @A2 */
  end                                                         /* @A2 */
                                                              /* @A2 */
  racflmsg = "Retrieving data - Please be patient"            /* @A2 */
  "control display lock"                                      /* @A2 */
  "display msg(RACF011)"                                      /* @A2 */
                                                              /* @A2 */
  call syscalls 'ON'                                          /* @A2 */
  address syscall                                             /* @A2 */
  numeric digits 12                                           /* @A2 */
  "getmntent m."                                              /* @A2 */
                                                              /* @A2 */
  CMDREC.1 = '%InUse Owner Filesystem / Mountpoint /',        /* @A2 */
             '(Mount-attributes)'                             /* @A2 */
  CMDREC.2 = '------ -----' copies('-',64)                    /* @A2 */
  NO = 2                                                      /* @A2 */
  l  = 0                                                      /* @A2 */
  do i = 1 to m.0                                             /* @A2 */
     sysname = strip(m.mnte_sysname.i)                        /* @A2 */
     upper sysname                                            /* @A2 */
     fsname = strip(m.mnte_fsname.i)                          /* @A2 */
     upper fsname                                             /* @A2 */
     path = strip(m.mnte_path.i)                              /* @A2 */
     upper path                                               /* @A2 */
     parm = strip(m.mnte_parm.i)                              /* @A2 */
     mode=''                                                  /* @A2 */
     if (m.mnte_mode.i > 255) then                            /* @A2 */
        m.mnte_mode.i = m.mnte_mode.i - 256                   /* @A2 */
     Call FLIST_Translate_Mode_Settings m.mnte_mode.i         /* @A2 */
     mode = mode||result                                      /* @A2 */
     if (string <> '') then do                                /* @A2 */
        if (substr(string,1,1) = '^'),                        /* @A2 */
         & (length(string) > 1) then do                       /* @A2 */
            stringx = substr(string,2)                        /* @A2 */
            if (pos(stringx,sysname) = 0),                    /* @A2 */
             & (pos(stringx,fsname)  = 0),                    /* @A2 */
             & (pos(stringx,path)    = 0),                    /* @A2 */
             & (pos(stringx,mode)    = 0),                    /* @A2 */
             & (pos(stringx,parm)    = 0) then                /* @A2 */
               call FLIST_get_usage                           /* @A2 */
        end                                                   /* @A2 */
        else do                                               /* @A2 */
           if (pos(string,sysname) > 0),                      /* @A2 */
            | (pos(string,fsname)  > 0),                      /* @A2 */
            | (pos(string,path)    > 0),                      /* @A2 */
            | (pos(string,mode)    > 0),                      /* @A2 */
            | (pos(string,parm)    > 0) then                  /* @A2 */
              call FLIST_get_usage                            /* @A2 */
        end                                                   /* @A2 */
     end                                                      /* @A2 */
     else                                                     /* @A2 */
        Call FLIST_Get_Usage                                  /* @A2 */
  end                                                         /* @A2 */
  NO = NO + 1                                                 /* @A2 */
  CMDREC.NO = COPIES("-",72)                                  /* @A2 */
  NO = NO + 1                                                 /* @A2 */
  CMDREC.NO = 'Total Filesystems Mounted =' m.0               /* @A2 */
  NO = NO + 1                                                 /* @A2 */
  CMDREC.NO = 'Total Filesystems Listed  =' l                 /* @A2 */
  CMDREC.0 = NO                                               /* @A2 */
RETURN                                                        /* @A2 */
/*--------------------------------------------------------------------*/
/*  FLIST - Get usage                                            @A2  */
/*--------------------------------------------------------------------*/
FLIST_GET_USAGE:                                              /* @A2 */
  statfs m.mnte_fsname.i s.                                   /* @A2 */
  if (DATATYPE(S.STFS_BLOCKSIZE) = "NUM") THEN DO             /* @A2 */
     j = s.stfs_total * s.stfs_blocksize                      /* @A2 */
     k = s.stfs_inuse * s.stfs_blocksize                      /* @A2 */
     p = trunc(k/j*100,2)                                     /* @A2 */
  END                                                         /* @A2 */
  if (p > percent) then do                                    /* @A2 */
     l  = l + 1                                               /* @A2 */
     NO = NO + 1                                              /* @A2 */
     CMDREC.NO = right(p,6) left(m.mnte_sysname.i,5),         /* @A2 */
                 left(m.mnte_fsname.i,44)                     /* @A2 */
     NO = NO + 1                                              /* @A2 */
     CMDREC.NO = copies(' ',12) left(m.mnte_path.i,64)        /* @A2 */
     NO = NO + 1                                              /* @A2 */
     CMDREC.NO = copies(' ',12) '('strip(mode)')'             /* @A2 */
     if (parm <> '') then do                                  /* @A2 */
        NO = NO + 1                                           /* @A2 */
        CMDREC.NO = copies(' ',12) 'PARM('strip(parm)')'      /* @A2 */
     end                                                      /* @A2 */
  end                                                         /* @A2 */
RETURN                                                        /* @A2 */
/*--------------------------------------------------------------------*/
/*  FLIST - Translate Mount Table Hex Mode Settings to Words     @A2  */
/*--------------------------------------------------------------------*/
FLIST_TRANSLATE_MODE_SETTINGS:                                /* @A6 */
  arg mode                                                    /* @A2 */
  mode_in_hex = d2c(mode)                                     /* @A2 */
  mode = ''                                                   /* @A2 */
  do bit = 7 to 0 by -1                                       /* @A2 */
     and_value  = d2c(2**(7-bit))                             /* @A2 */
     bit_status = bitand(mode_in_hex,and_value) > '00'x       /* @A2 */
     fsm.bit    = fsm.bit.bit_status                          /* @A2 */
     if (fsm.bit <> '') then mode = mode||fsm.bit' '          /* @A2 */
  end                                                         /* @A2 */
RETURN MODE                                                   /* @A2 */
/*--------------------------------------------------------------------*/
/*  FLIST - Resolve path name                                    @A6  */
/*--------------------------------------------------------------------*/
FLIST_RESOLVE_PATH_NAME:                                      /* @A4 */
  ID = CHKPRM                                                 /* @A4 */
  call syscalls 'ON'                                          /* @A4 */
  address syscall                                             /* @A4 */
  'stat (id) st.'                                             /* @A4 */
  if (rc <> 0) & (retval = -1) then do                        /* @A6 */
     cmdrec.1 = 'Unable to find path' id                      /* @A4 */
     cmdrec.0 = 1                                             /* @A4 */
     return                                                   /* @A6 */
  end                                                         /* @A6 */
                                                              /* @A6 */
  "getmntent a."                                              /* @A4 */
  'realpath (id) path'                                        /* @A4 */
  if (rc <> 0) | (retval = -1) then do                        /* @A4 */
     cmdrec.1 = 'Unable to resolve path for' id               /* @A4 */
     cmdrec.0 = 1                                             /* @A4 */
     return                                                   /* @A4 */
  end                                                         /* @A4 */
                                                              /* @A6 */
  cmdrec.1 = 'Specified path:' id                             /* @A4 */
  cmdrec.2 = 'Resolved  path:' path                           /* @A4 */
  cmdrec.3 = ' '                                              /* @A4 */
  cmdrec.4 = 'Resolved path dissection:'                      /* @A4 */
  cmdrec.5 = 'UGO Owning-User(Uid)   Owning-Group(Gid) ',     /* @A4 */
             'Path node'                                      /* @A4 */
  cmdrec.6 = '--- ------------------ ------------------',     /* @A4 */
             '--------->'                                     /* @A4 */
  no = 6                                                      /* @A4 */
  pp = ''                                                     /* @A4 */
  do while path <> ''                                         /* @A4 */
     parse var path p '/' path                                /* @A4 */
     pp = pp || '/' || p                                      /* @A4 */
     'stat (pp) st.'                                          /* @A4 */
     pw.pw_name = '-undef-'                                   /* @A4 */
     'getpwuid (st.st_uid) pw.'                               /* @A4 */
     uid = strip(pw.pw_name)'('st.st_uid')'                   /* @A4 */
     gr.gr_name = '-undef-'                                   /* @A4 */
     'getgrgid (st.st_gid) gr.'                               /* @A4 */
     gid=strip(gr.gr_name)'('st.st_gid')'                     /* @A4 */
     NO = NO + 1                                              /* @A4 */
     CMDREC.NO = st.st_mode left(uid,18) left(gid,18) pp      /* @A4 */
     call flist_chkmntpt                                      /* @A4 */
     if (result = 4) then do                                  /* @A4 */
        if (a.MNTE_MODE.i > 255) then                         /* @A4 */
           a.MNTE_MODE.i = a.MNTE_MODE.i - 256                /* @A4 */
        Call FLIST_Translate_Mode_Settings a.MNTE_MODE.i      /* @A4 */
        mode = result                                         /* @A4 */
        NO   = NO + 1                                         /* @A4 */
        CMDREC.NO = '    FileSys='a.MNTE_FSNAME.i             /* @A4 */
        NO   = NO + 1                                         /* @A4 */
        CMDREC.NO = '    Owner='left(a.MNTE_SYSNAME.i,5),     /* @A4 */
                    ' Mode=('strip(mode)')'                   /* @A4 */
     end                                                      /* @A4 */
     if (pp = '/') then pp = ''                               /* @A4 */
  end                                                         /* @A4 */
RETURN                                                        /* @A4 */
/*--------------------------------------------------------------------*/
/*  FLIST - Check mount point                                    @A6  */
/*--------------------------------------------------------------------*/
FLIST_CHKMNTPT:                                               /* @A4 */
  do i=a.0 to 1 by -1                                         /* @A4 */
     if (a.MNTE_PATH.i = pp) then                             /* @A4 */
        return 4                                              /* @A4 */
  end                                                         /* @A4 */
RETURN 0                                                      /* @A4 */
