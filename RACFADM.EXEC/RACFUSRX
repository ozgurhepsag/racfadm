/*%NOCOMMENT====================* REXX *==============================*/
/*  PURPOSE:  RACFADM - User (Option 1) - Cross Ref. Rpt (linecmd XR) */
/*--------------------------------------------------------------------*/
/* FLG  YYMMDD  USERID   DESCRIPTION                                  */
/* ---  ------  -------  -------------------------------------------- */
/* @A4  200412  RACFA    Chg TRACE to allow 'L'abels or 'R'esults     */
/* @A3  200301  RACFA    Standardize messaging                        */
/* @A2  200221  RACFA    Make 'ADDRESS ISPEXEC' defualt, reduce code  */
/* @A1  200220  RACFA    Added ERATRACE=YES, then TRACE R             */
/* @A0  200123  TRIDJK   Created REXX program                         */
/*====================================================================*/
ADDRESS ISPEXEC                                               /* @A2 */
  "VGET (ERATRACE) ASIS"                                      /* @A1 */
  If (eratrace <> 'NO') then do                               /* @A4 */
     parse source . . REXXPGM .                               /* @A4 */
     Say "*"COPIES("-",70)"*"                                 /* @A4 */
     Say "*"Center("Begin Program = "REXXPGM,70)"*"           /* @A4 */
     Say "*"COPIES("-",70)"*"                                 /* @A4 */
     interpret "Trace "SUBSTR(ERATRACE,1,1)                   /* @A4 */
  end                                                         /* @A4 */
  /*--------------------------------------------*/
  /*  Call IRRUT100 for cross reference report  */
  /*--------------------------------------------*/
  Parse Arg user

Address TSO
  x = outtrap('delete.','*')
  'delete irrut100.'result
  x = outtrap('off')
  'alloc f(sysprint) new reuse unit(sysallda)',
         'space(1,1) tracks'
  'alloc f(sysut1)   new reuse unit(sysallda)',
         'space(1,1) tracks'
  'alloc f(sysin)    new reuse unit(sysallda)',
         'lrecl(80) blksize(0) recfm(f b)',
         'space(1,1) tracks'
  'newstack'
  queue user
  queue
  'Execio * diskw sysin (finis'
  "call *(irrut100)"
  'Execio * diskr sysprint (finis stem sysp.'

  /*---------------------*/
  /*  Browse the report  */
  /*---------------------*/
Address ISPExec
  'lminit dataid(id) ddname(sysprint) enq(exclu)'
  if (rc ^= 0) then do
     racfsmsg = 'Allocation error'                            /* @A3 */
     racflmsg = 'LMINIT failed for SYSPRINT'                  /* @A3 */
     'setmsg msg(racf001)'                                    /* @A3 */
     call Goodbye                                             /* @DK */
  end
  'Browse dataid('id')'
  'lmfree dataid('id')'

Address TSO
  'delstack'
  'free  f(sysut1 sysprint sysin)'
  'alloc f(sysin) ds(*) reuse'

  call Goodbye
EXIT
/*--------------------------------------------------------------------*/
/*  If tracing is on, display flower box                         @DK  */
/*--------------------------------------------------------------------*/
GOODBYE:                                                      /* @DK */
  If (eratrace <> 'NO') then do                               /* @DK */
     Say "*"COPIES("-",70)"*"                                 /* @DK */
     Say "*"Center("End Program = "REXXPGM,70)"*"             /* @DK */
     Say "*"COPIES("-",70)"*"                                 /* @DK */
  end                                                         /* @DK */
EXIT                                                          /* @DK */
