/*%NOCOMMENT====================* REXX *==============================*/
/*  PURPOSE:  RACFADM - User (Option 1) - Cross Ref. Rpt (linecmd XR) */
/*--------------------------------------------------------------------*/
/* FLG  YYMMDD  USERID   DESCRIPTION                                  */
/* ---  ------  -------  -------------------------------------------- */
/* @AA  200429  RACFA    Re-arranged variables (General, Mgmt, TSO)   */
/* @A9  200423  RACFA    Move PARSE REXXPGM name up above IF SETMTRAC */
/* @A8  200423  RACFA    Ensure REXX program name is 8 chars long     */
/* @A7  200422  TRIDJK   Added capability to browse/edit/view file    */
/* @A6  200417  RACFA    Removed 'enq(exclu)', not needed             */
/* @A5  200413  RACFA    Chg TRACEing to only display banner (P=Pgms) */
/* @A4  200412  RACFA    Chg TRACE to allow 'L'abels or 'R'esults     */
/* @A3  200301  RACFA    Standardize messaging                        */
/* @A2  200221  RACFA    Make 'ADDRESS ISPEXEC' defualt, reduce code  */
/* @A1  200220  RACFA    Added SETMTRAC=YES, then TRACE R             */
/* @A0  200123  TRIDJK   Created REXX program                         */
/*====================================================================*/
EDITMACR = "RACFEMAC"    /* Edit Macro, turn HILITE off    */ /* @A7 */
parse source . . REXXPGM .                                    /* @A9 */
REXXPGM  = LEFT(REXXPGM,8)                                    /* @A9 */

ADDRESS ISPEXEC                                               /* @A2 */
  "VGET (SETGDISP SETMTRAC) ASIS"                             /* @AA */
  If (SETMTRAC <> 'NO') then do                               /* @A4 */
     Say "*"COPIES("-",70)"*"                                 /* @A4 */
     Say "*"Center("Begin Program = "REXXPGM,70)"*"           /* @A4 */
     Say "*"COPIES("-",70)"*"                                 /* @A4 */
     if (SETMTRAC <> 'PROGRAMS') THEN                         /* @A5 */
        interpret "Trace "SUBSTR(SETMTRAC,1,1)                /* @A4 */
  end                                                         /* @A4 */
  /*--------------------------------------------*/
  /*  Call IRRUT100 for cross reference report  */
  /*--------------------------------------------*/
  Parse Arg user

Address TSO
  x = outtrap('delete.','*')
  'delete irrut100.'result
  x = outtrap('off')
  'alloc f(sysprint) new reuse unit(sysallda)',
         'space(1,1) tracks'
  'alloc f(sysut1)   new reuse unit(sysallda)',
         'space(1,1) tracks'
  'alloc f(sysin)    new reuse unit(sysallda)',
         'lrecl(80) blksize(0) recfm(f b)',
         'space(1,1) tracks'
  'newstack'
  queue user
  queue
  'Execio * diskw sysin (finis'
  "call *(irrut100)"
  'Execio * diskr sysprint (finis stem sysp.'

  /*---------------------*/
  /*  Browse the report  */
  /*---------------------*/
Address ISPExec
  'lminit dataid(id) ddname(sysprint)'                        /* @A6 */
  if (rc ^= 0) then do
     racfsmsg = 'Allocation error'                            /* @A3 */
     racflmsg = 'LMINIT failed for SYSPRINT'                  /* @A3 */
     'setmsg msg(racf001)'                                    /* @A3 */
     call Goodbye                                             /* @DK */
  end
  Select                                                      /* @A7 */
     When (SETGDISP = "VIEW") THEN                            /* @A7 */
          "VIEW DATAID("id") MACRO("EDITMACR")"               /* @A7 */
     When (SETGDISP = "EDIT") THEN                            /* @A7 */
          "EDIT DATAID("id") MACRO("EDITMACR")"               /* @A7 */
     Otherwise                                                /* @A7 */
          'browse dataid('id')'                               /* @A7 */
  end                                                         /* @A7 */
  'lmfree dataid('id')'

Address TSO
  'delstack'
  'free  f(sysut1 sysprint sysin)'
  'alloc f(sysin) ds(*) reuse'

  call Goodbye
EXIT
/*--------------------------------------------------------------------*/
/*  If tracing is on, display flower box                         @DK  */
/*--------------------------------------------------------------------*/
GOODBYE:                                                      /* @DK */
  If (SETMTRAC <> 'NO') then do                               /* @DK */
     Say "*"COPIES("-",70)"*"                                 /* @DK */
     Say "*"Center("End Program = "REXXPGM,70)"*"             /* @DK */
     Say "*"COPIES("-",70)"*"                                 /* @DK */
  end                                                         /* @DK */
EXIT                                                          /* @DK */
