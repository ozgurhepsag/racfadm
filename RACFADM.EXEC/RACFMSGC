/*%NOCOMMENT====================* REXX *==============================*/
/*  PURPOSE:  RACFADM - Display 'Confirm Request' pop-up panel        */
/*--------------------------------------------------------------------*/
/* FLG  YYMMDD  USERID   DESCRIPTION                                  */
/* ---  ------  -------  -------------------------------------------- */
/* @A8  200412  RACFA    Chg TRACE to allow 'L'abels or 'R'esults     */
/* @A7  200228  RACFA    Removed ERASHOW from VGET, not used          */
/* @A6  200224  RACFA    Standardize quotes, chg single to double     */
/* @A5  200224  RACFA    Place panels at top of REXX in variables     */
/* @A4  200223  RACFA    Del 'address TSO "PROFILE MSGID"', not needed*/
/* @A3  200220  RACFA    Added ERATRACE=YES, then TRACE R             */
/* @A2  200218  RACFA    Condense VGETs into one line                 */
/* @A1  200119  RACFA    Standardized/reduced lines of code           */
/* @A0  011229  NICORIZ  Created REXX, V2.1, www.rizzuto.it           */
/*====================================================================*/
PANELM1 = "RACFMSG1"    /* Confirm Request (pop-up)        */ /* @A5 */

ADDRESS ISPEXEC                                               /* @A3 */
  Arg message
  Signal off error
  "VGET (ERATRACE) ASIS"                                      /* @A7 */
  If (eratrace <> 'NO') then do                               /* @A8 */
     parse source . . REXXPGM .                               /* @A8 */
     Say "*"COPIES("-",70)"*"                                 /* @A8 */
     Say "*"Center("Begin Program = "REXXPGM,70)"*"           /* @A8 */
     Say "*"COPIES("-",70)"*"                                 /* @A8 */
     interpret "Trace "SUBSTR(ERATRACE,1,1)                   /* @A8 */
  end                                                         /* @A8 */

  answer  = 'NO'
  zwinttl = 'CONFIRM REQUEST'
  Do until (ckey = 'PF03') | (ckey = 'ENTER')
     'CONTROL NOCMD'
     "ADDPOP"                                                 /* @A6 */
     "DISPLAY PANEL("PANELM1")"                               /* @A5 */
     "REMPOP"                                                 /* @A6 */
  End
  Select
     when (ckey = 'PF03')  then answer = 'NO'
     when (ckey = 'ENTER') then answer = 'YES'
     otherwise nop
  End
  zwinttl = ' '

  If (eratrace <> 'NO') then do                               /* @A8 */
     Say "*"COPIES("-",70)"*"                                 /* @A8 */
     Say "*"Center("End Program = "REXXPGM,70)"*"             /* @A8 */
     Say "*"COPIES("-",70)"*"                                 /* @A8 */
  end                                                         /* @A8 */
RETURN answer
