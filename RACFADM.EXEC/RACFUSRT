/*%NOCOMMENT====================* REXX *==============================*/
/*  PURPOSE:  RACFADM - Userid profiles - Create TSO alias/datasets   */
/*--------------------------------------------------------------------*/
/* FLG  YYMMDD  USERID   DESCRIPTION                                  */
/* ---  ------  -------  -------------------------------------------- */
/* @AA  200125  LBD      Fixed PREFIX, wrong environment              */
/* @A9  200124  RACFA    Place panels at top of REXX in variables     */
/* @A8  200223  RACFA    Del 'address TSO "PROFILE MSGID"', not needed*/
/* @A7  200221  RACFA    Make 'ADDRESS ISPEXEC' defualt, reduce code  */
/* @A6  200220  RACFA    Added ERATRACE=YES, then TRACE R             */
/* @A5  200220  RACFA    Renamed RACFTSO to RACFUSRT (Standardization)*/
/* @A4  200218  RACFA    Condense VGETs into one line                 */
/* @A3  200119  RACFA    Standardized/reduced lines of code           */
/* @A2  200119  RACFA    Added comment box above procedures           */
/* @A1  200118  RACFA    Fixed NOPREF issue with users TSO PROFILE    */
/* @A0  011229  NICORIZ  Created REXX, V2.1, www.rizzuto.it           */
/*====================================================================*/
PANEL29 = "RACFUSRT"    /* Add, chg and del a TSO userid  */  /* @A9 */

ADDRESS ISPEXEC                                               /* @A7 */
  signal off error
  era_cmd = arg(1)
  user    = arg(2)
  tsoproc = arg(3)
  tsoacct = arg(4)
  defgrp  = arg(5)
  owner   = arg(6)
  joblib  = arg(7)
  "VGET (ERASHOW ERATRACE ERAISPP ERACTLG",                   /* @A4 */
        "ERAUNIT ERAULIB  ERAUVOL ERAMCAT) ASIS"              /* @A4 */
  If (eratrace = 'YES') then Trace r                          /* @A6 */

ADDRESS TSO
  "PROF NOPREF"                                               /* @AA */
  signal on error
  x = OUTTRAP('trash.')
  interpret 'signal ' era_cmd

/*--------------------------------------------------------------------*/
/*  Add/change userid datasets                                        */
/*--------------------------------------------------------------------*/
ADDD:
CHGD:
ADDRESS TSO
  "ADDSD  ('"user".**') UACC(READ) OWNER("owner")"
/*-------------------------------------------------------------------*/
/*- Authorize sys1.brodcast update for alias creation -              */
/*    "PERMIT 'SYS1.BRODCAST' GEN ACC(UPDATE) ID("USERID()")"        */
/*- Authorize volume allocation for TSO libraries                    */
/*    "PERMIT $ALLOC."erauvol" CLASS(USRDASD)"                       */
/*                  "ACC(UPDATE) ID("USERID()")"                     */
/*    "SETROPTS RACLIST(USRDASD) REFRESH"                            */
/*- Authorize MASTER catalog for alias creation                      */
/*    "PERMIT '"eramcat"' GEN ACC(ALTER) ID("USERID()")"             */
/*-------------------------------------------------------------------*/

  /*- Authorize Library allocation                -*/
  "PERMIT '"user".**' GEN ACC(ALTER) ID("USERID()")"
  "SETROPTS REFRESH GENERIC(DATASET)"

  /*- Define user alias for datasets -*/
  signal off error
  "DEL '"user"' ALIAS PURGE"
  signal on error
  "DEF ALIAS (NAME("user") RELATE("eractlg"))"

  /*- Allocate ISPF profile          -*/
  signal off error
/*-------------------------------------------------------------------*/
/* "ALLOC FILE(DD1) DATASET('"user"."eraispp"') ",                   */
/*   "UNIT("eraunit") VOLUME("erauvol") NEW SPACE(1,1) TRACKS ",     */
/*   "BLKSIZE(6400) LRECL(80) RECFM(F B) CATALOG DIR(15)"            */
/*-------------------------------------------------------------------*/
  "ALLOC FILE(DD1) DATASET('"user"."eraispp"') ",
         "NEW SPACE(1,1) TRACKS ",
         "BLKSIZE(6400) LRECL(80) RECFM(F B) CATALOG DIR(15)"
  "FREE FILE(DD1)"

  /*- Allocate joblib library        -*/
  if joblib='YES' then do
    "ALLOC FILE(DD2) DATASET('"user"."eraulib"') ",
         "NEW SPACE(1,1) TRACKS ",
         "BLKSIZE(6400) LRECL(80) RECFM(F B) CATALOG DIR(15)"
    "FREE FILE(DD2)"
  end

  signal on error
  /*- Authorize logon and account to default group  -*/
  "PE "tsoacct" CLASS(ACCTNUM) ACC(READ) ID("defgrp")"
  "PE "tsoproc" CLASS(TSOPROC) ACC(READ) ID("defgrp")"
  "SETROPTS RACLIST(ACCTNUM) REFRESH"
  "SETROPTS RACLIST(TSOPROC) REFRESH"

  call Delete_Admin_permits
  "PROFILE PREF("USERID()")"                                  /* @A1 */
RETURN RC
/*--------------------------------------------------------------------*/
/*  Delete userid dataset profile                                     */
/*--------------------------------------------------------------------*/
DELD:
ADDRESS TSO                                                   /* @A7 */
  signal off error
  "DELDSD  ('"user".**')"
  "DELETE '"user"."eraulib"'"
  "DELETE '"user"."eraispp"'"
  "DELETE ("user") ALIAS"
  signal on error
RETURN RC
/*--------------------------------------------------------------------*/
/*  Display message                                                   */
/*--------------------------------------------------------------------*/
DISPLAY_MSG:
ADDRESS ISPEXEC                                               /* @A7 */
  trash_msg = trash.0
  "VGET (ZSCREEN) ASIS"
  tblmsg = 'RCFER'ZSCREEN /* make table unique */
  "TBCREATE" tblmsg "KEYS(MSG) REPLACE NOWRITE"
  do trash_count=1 to trash_msg
     msg= trash.trash_count
     "TBMOD" tblmsg
  end
  "TBTOP  " tblmsg
  "TBDISPL" tblmsg "PANEL("PANEL29")"                         /* @A9 */
  "TBEND  " tblmsg
ADDRESS TSO                                                   /* @AA */
RETURN
/*--------------------------------------------------------------------*/
/*  Delete userid dataset profile                                     */
/*--------------------------------------------------------------------*/
ERROR:
ADDRESS TSO                                                   /* @A7 */
  call Display_msg
  "DELDSD  ('"user".**')"
  call Delete_Admin_permits
RETURN 8
/*--------------------------------------------------------------------*/
/*  Delete temporary autorizations for RACF administrator             */
/*--------------------------------------------------------------------*/
DELETE_ADMIN_PERMITS:
ADDRESS TSO                                                   /* @A7 */
  "PERMIT '"user".**'     GEN DELETE ID("USERID()")"
/*-------------------------------------------------------------------*/
/*  "PERMIT 'SYS1.BRODCAST' GEN DELETE ID("USERID()")"               */
/*  "PERMIT '"eramcat"'     GEN DELETE ID("USERID()")"               */
/*  "PERMIT $ALLOC."erauvol" CLASS(USRDASD) DELETE ID("USERID()")"   */
/*  "SETROPTS RACLIST(USRDASD) REFRESH"                              */
/*-------------------------------------------------------------------*/
  "SETROPTS REFRESH GENERIC(DATASET)"
RETURN
