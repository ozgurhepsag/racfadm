/*%NOCOMMENT====================* REXX *==============================*/
/*  PURPOSE:  RACFADM - Userid profiles - Create TSO alias/datasets   */
/*--------------------------------------------------------------------*/
/* FLG  YYMMDD  USERID   DESCRIPTION                                  */
/* ---  ------  -------  -------------------------------------------- */
/* @AH  200228  RACFA    Removed ARG() statements, use PARSE ARG      */
/* @AG  200228  RACFA    Allow turning disp. REXX cmds 'Settings (0)' */
/* @AF  200228  RACFA    Allow turning tracing on from 'Settings (0)' */
/* @AE  200227  RACFA    If EGN=YES, then 2 asterisks, else one       */
/* @AD  200227  RACFA    Set info messages off, del outtrap('trash.') */
/* @AC  200227  TRIDJK   Add single quotes around catalog name        */
/* @AB  200226  RACFA    Removed "PROFILE PREF("USERID()")"           */
/* @AB  200226  RACFA    Removed 'ADDRESS TSO PROF NOPREFIX'          */
/* @AA  200225  LBD      Fixed PREFIX, wrong environment              */
/* @A9  200224  RACFA    Place panels at top of REXX in variables     */
/* @A8  200223  RACFA    Del 'address TSO "PROFILE MSGID"', not needed*/
/* @A7  200221  RACFA    Make 'ADDRESS ISPEXEC' defualt, reduce code  */
/* @A6  200220  RACFA    Added ERATRACE=YES, then TRACE R             */
/* @A5  200220  RACFA    Renamed RACFTSO to RACFUSRT (Standardization)*/
/* @A4  200218  RACFA    Condense VGETs into one line                 */
/* @A3  200119  RACFA    Standardized/reduced lines of code           */
/* @A2  200119  RACFA    Added comment box above procedures           */
/* @A1  200118  RACFA    Fixed NOPREF issue with users TSO PROFILE    */
/* @A0  011229  NICORIZ  Created REXX, V2.1, www.rizzuto.it           */
/*====================================================================*/
PANEL29 = "RACFUSRT"    /* Add, chg and del a TSO userid   */  /* @A9 */
PANELM2 = "RACFMSG2"    /* Execute RACF Command (pop-up)   */  /* @AW */
X       = MSG("OFF")    /* No informational messages       */  /* @AD */

ADDRESS ISPEXEC                                               /* @AF */
  "VGET (ERASHOW ERATRACE ERAISPP ERACTLG",                   /* @A4 */
        "ERAUNIT ERAULIB  ERAUVOL ERAMCAT) ASIS"              /* @A4 */
  If (eratrace = 'YES') then Trace r                          /* @A6 */

  rc      = GETEGN(ERAEGN,53,'01','NO','YES')  /* egn=Y/N */  /* @AE */
  ASTERIC = "*"                                               /* @AE */
  IF (ERAEGN = "YES") THEN                                    /* @AE */
     ASTERIC = "**"                                           /* @AE */

  signal off error
  parse arg era_cmd user tsoproc tsoacct defgrp owner joblib  /* @AH */

  signal on error
  interpret 'signal ' era_cmd
/*--------------------------------------------------------------------*/
/*  Add/change userid datasets                                        */
/*--------------------------------------------------------------------*/
ADDD:
CHGD:
  cmd = "ADDSD  ('"user"."ASTERIC"') UACC(READ)",             /* @AG */
        "OWNER("owner")"                                      /* @AG */
  call exccmd                                                 /* @AG */
/*-------------------------------------------------------------------*/
/*- Authorize sys1.brodcast update for alias creation -              */
/*    "PERMIT 'SYS1.BRODCAST' GEN ACC(UPDATE) ID("USERID()")"        */
/*- Authorize volume allocation for TSO libraries                    */
/*    "PERMIT $ALLOC."erauvol" CLASS(USRDASD)"                       */
/*                  "ACC(UPDATE) ID("USERID()")"                     */
/*    "SETROPTS RACLIST(USRDASD) REFRESH"                            */
/*- Authorize MASTER catalog for alias creation                      */
/*    "PERMIT '"eramcat"' GEN ACC(ALTER) ID("USERID()")"             */
/*-------------------------------------------------------------------*/

  /*- Authorize Library allocation                -*/
  cmd = "PERMIT '"user"."ASTERIC"' GEN ACC(ALTER)",           /* @AG */
                 "ID("USERID()")"                             /* @AG */
  call exccmd                                                 /* @AG */
  cmd = "SETROPTS REFRESH GENERIC(DATASET)"                   /* @AG */
  call exccmd                                                 /* @AG */

  /*- Define user alias for datasets -*/
  signal off error
  cmd = "DEL '"user"' ALIAS PURGE"                            /* @AG */
  call exccmd                                                 /* @AG */
  signal on error
  Eractlg = Strip(eractlg,b,"'")                              /* @AC */
  cmd = "DEF ALIAS (NAME("user") RELATE('"eractlg"'))"        /* @AC */
  call exccmd                                                 /* @AG */

  /*- Allocate ISPF profile          -*/
  signal off error
/*-------------------------------------------------------------------*/
/* "ALLOC FILE(DD1) DATASET('"user"."eraispp"') ",                   */
/*   "UNIT("eraunit") VOLUME("erauvol") NEW SPACE(1,1) TRACKS ",     */
/*   "BLKSIZE(6400) LRECL(80) RECFM(F B) CATALOG DIR(15)"            */
/*-------------------------------------------------------------------*/
  cmd = "ALLOC FILE(DD1) DATASET('"user"."eraispp"')",        /* @AG */
              "NEW SPACE(5,5) CYLINDERS",                     /* @AG */
              "BLKSIZE(0) LRECL(80) RECFM(F B)",              /* @AG */
              "CATALOG DIR(250)"                              /* @AG */
  call exccmd                                                 /* @AG */
  cmd = "FREE FILE(DD1)"                                      /* @AG */
  call exccmd                                                 /* @AG */

  /*- Allocate joblib library        -*/
  if joblib='YES' then do
     cmd = "ALLOC FILE(DD2) DATASET('"user"."eraulib"')",     /* @AG */
                  "NEW SPACE(5,5) CYLINDERS",                 /* @AG */
                  "BLKSIZE(0) LRECL(80) RECFM(F B)",          /* @AG */
                  "CATALOG DIR(250)"                          /* @AG */
     call exccmd                                              /* @AG */
     cmd = "FREE FILE(DD2)"                                   /* @AG */
     call exccmd                                              /* @AG */
  end

  signal on error
  /*- Authorize logon and account to default group  -*/
  cmd = "PE "tsoacct" CLASS(ACCTNUM) ACC(READ) ID("defgrp")"  /* @AG */
  call exccmd                                                 /* @AG */
  cmd = "PE "tsoproc" CLASS(TSOPROC) ACC(READ) ID("defgrp")"  /* @AG */
  call exccmd                                                 /* @AG */
  cmd = "SETROPTS RACLIST(ACCTNUM) REFRESH"                   /* @AG */
  call exccmd                                                 /* @AG */
  cmd = "SETROPTS RACLIST(TSOPROC) REFRESH"                   /* @AG */
  call exccmd                                                 /* @AG */

  call Delete_Admin_permits
RETURN RC
/*--------------------------------------------------------------------*/
/*  Delete userid dataset profile                                     */
/*--------------------------------------------------------------------*/
DELD:
  signal off error
  cmd = "DELDSD  ('"user"."ASTERIC"')"                        /* @AG */
  call exccmd                                                 /* @AG */
  cmd = "DELETE '"user"."eraulib"'"                           /* @AG */
  call exccmd                                                 /* @AG */
  cmd = "DELETE '"user"."eraispp"'"                           /* @AG */
  call exccmd                                                 /* @AG */
  cmd = "DELETE ("user") ALIAS"                               /* @AG */
  call exccmd                                                 /* @AG */
  signal on error
RETURN RC
/*--------------------------------------------------------------------*/
/*  Delete userid dataset profile                                     */
/*--------------------------------------------------------------------*/
ERROR:
  call Display_msg
  cmd = "DELDSD  ('"user"."ASTERIC"')"                        /* @AG */
  call exccmd                                                 /* @AG */
  call Delete_Admin_permits
RETURN 8
/*--------------------------------------------------------------------*/
/*  Delete temporary autorizations for RACF administrator             */
/*--------------------------------------------------------------------*/
DELETE_ADMIN_PERMITS:
  cmd = "PERMIT '"user"."ASTERIC"' GEN DELETE",               /* @AG */
                 "ID("USERID()")"                             /* @AG */
  call exccmd                                                 /* @AG */
/*-------------------------------------------------------------------*/
/*  "PERMIT 'SYS1.BRODCAST' GEN DELETE ID("USERID()")"               */
/*  "PERMIT '"eramcat"'     GEN DELETE ID("USERID()")"               */
/*  "PERMIT $ALLOC."erauvol" CLASS(USRDASD) DELETE ID("USERID()")"   */
/*  "SETROPTS RACLIST(USRDASD) REFRESH"                              */
/*-------------------------------------------------------------------*/
  cmd = "SETROPTS REFRESH GENERIC(DATASET)"                   /* @AG */
  call exccmd                                                 /* @AG */
RETURN
/*--------------------------------------------------------------------*/
/*  Enhanced Generic Name (EGN) = Yes or No                           */
/*--------------------------------------------------------------------*/
GETEGN:
  variable = arg(1)
  offset   = arg(2)
  value    = arg(3)
  status1  = arg(4)
  status2  = arg(5)
  cvt      = c2x(storage(10,4))      /* cvt address        */
  cvtrac$  = d2x((x2d(cvt))+992)     /* cvt+3E0 = cvtrac $ */
  cvtrac   = c2x(storage(cvtrac$,4)) /* cvtrac=access cntl */
  rcvtsta$ = d2x((x2d(cvtrac))+53)
  interpret "rcvtsta$= d2x((x2d("cvtrac"))+"offset")"
  x        = storage(rcvtsta$,1)
  interpret variable '= 'status1
  interpret "x=bitand(x,'"value"'x)" /* del unwanted bits*/
  interpret "if (x= '"value"'x) then "variable"="status2
RETURN 0000
/*--------------------------------------------------------------------*/
/*  Display RACF command                                          @BB */
/*--------------------------------------------------------------------*/
EXCCMD:                                                       /* @AG */
ADDRESS TSO                                                   /* @AG */
  cmd                                                         /* @AG */
  cmd_rc = rc                                                 /* @AG */
  if (erashow = 'YES') then                                   /* @AG */
     call SHOWCMD                                             /* @AG */
RETURN                                                        /* @AG */
/*--------------------------------------------------------------------*/
/*  Display RACF command                                          @BB */
/*--------------------------------------------------------------------*/
SHOWCMD:                                                      /* @BB */
ADDRESS ISPEXEC                                               /* @AG */
  PARSE VAR CMD MSG1 60 MSG2 121 MSG3                         /* @BB */
  MSG4 = "Return code = "cmd_rc                               /* @BS */
  "ADDPOP ROW(6) COLUMN(4)"                                   /* @BB */
  "DISPLAY PANEL("PANELM2")"                                  /* @BL */
  "REMPOP"                                                    /* @BB */
RETURN                                                        /* @BB */
/*--------------------------------------------------------------------*/
/*  Display message                                                   */
/*--------------------------------------------------------------------*/
DISPLAY_MSG:
ADDRESS ISPEXEC                                               /* @A7 */
  trash_msg = trash.0
  "VGET (ZSCREEN) ASIS"
  tblmsg = 'RCFER'ZSCREEN /* make table unique */
  "TBCREATE" tblmsg "KEYS(MSG) REPLACE NOWRITE"
  do trash_count=1 to trash_msg
     msg= trash.trash_count
     "TBMOD" tblmsg
  end
  "TBTOP  " tblmsg
  "TBDISPL" tblmsg "PANEL("PANEL29")"                         /* @A9 */
  "TBEND  " tblmsg
RETURN
