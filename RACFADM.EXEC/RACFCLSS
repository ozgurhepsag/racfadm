/*%NOCOMMENT====================* REXX *==============================*/
/*  PURPOSE:  RACFADM - General resources - Search classes            */
/*--------------------------------------------------------------------*/
/* FLG  YYMMDD  USERID   DESCRIPTION                                  */
/* ---  ------  -------  -------------------------------------------- */
/* @B5  200417  RACFA    Chg REXX pgm name RACFAUTH to RACFCLSA       */
/* @B4  200413  RACFA    Chg TRACEing to only display banner (P=Pgms) */
/* @B3  200412  RACFA    Deleted subroutine 'ERROR', not needed       */
/* @B2  200412  RACFA    Chg TRACE to allow 'L'abels or 'R'esults     */
/* @B1  200402  RACFA    Chg LRECL=132 to LRECL=80                    */
/* @AZ  200402  RACFA    Fixed VIEW/EDIT, was missing edit macro var. */
/* @AY  200401  RACFA    Create subroutine to VIEW/EDIT/BROWSE        */
/* @AX  200401  RACFA    VIEW/EDIT use edit macro, to turn off HILITE */
/* @AW  200330  RACFA    Allow point-n-shoot sort ascending/descending*/
/* @AV  200324  RACFA    Allow both display/logging of RACF commands  */
/* @AU  200324  RACFA    Allow logging RACF commands to ISPF Log file */
/* @AT  200303  RACFA    Chg 'RL class ALL' to 'RL class * ALL'       */
/* @AS  200303  RACFA    Added line cmd 'L-List' to class's profile   */
/* @AR  200303  RACFA    Added line cmd 'L-List' to class             */
/* @AQ  200302  RACFA    Fixed SORT cmd for class profiles            */
/* @AP  200302  RACFA    Chk/honor 'Status Interval' for cls profiles */
/* @AO  200302  RACFA    Add SORT/LOCATE/ONLY/RESET for class profiles*/
/* @AN  200301  RACFA    Chk for 'NO ENTRIES MEET SEARCH CRITERIA'    */
/* @AM  200226  RACFA    Fix @AK chg, chg ret_code to cmd_rc          */
/* @AL  200226  RACFA    Added 'CONTROL ERRORS RETURN'                */
/* @AK  200226  RACFA    Removed double quotes before/after cmd       */
/* @AJ  200225  RACFA    Del address TSO "PROFILE PREF("USERID()")"   */
/* @AI  200224  RACFA    Fixed PANEL08, typo in defining variable     */
/* @AH  200224  RACFA    Standardize quotes, chg single to double     */
/* @AG  200224  RACFA    Place panels at top of REXX in variables     */
/* @AF  200223  RACFA    Del 'address TSO "PROFILE MSGID"', not needed*/
/* @AE  200223  RACFA    Reduced code when LOCATEing, not necessary   */
/* @AD  200223  RACFA    Reduced code when SORTing, not necessary     */
/* @AC  200223  RACFA    Created RACFCLSD (only Class, no profile col)*/
/* @AB  200223  RACFA    Added primary cmds: SORT, LOCATE, ONLY, RESET*/
/* @AA  200222  RACFA    Removed translating OPTA/B, not needed       */
/* @A9  200222  RACFA    Allow placing cursor on row and press ENTER  */
/* @A8  200221  RACFA    Removed "G = '(G)'", not referenced          */
/* @A7  200221  RACFA    Get ERASHOW, to display RACF cmds            */
/* @A6  200221  RACFA    Make 'ADDRESS ISPEXEC' defualt, reduce code  */
/* @A5  200220  RACFA    Fixed displaying all RACF commands           */
/* @A4  200220  RACFA    Added ERATRACE=YES, then TRACE R             */
/* @A3  200214  RACFA    Chged panel name RACFSRCH to RACFCLS8        */
/* @A2  200120  RACFA    Standardized/reduced lines of code           */
/* @A1  200120  RACFA    Added comment box above procedures           */
/* @A0  011229  NICORIZ  Created REXX, V2.1, www.rizzuto.it           */
/*====================================================================*/
PANEL08  = "RACFCLS8"    /* List classes and profiles      */ /* @AI */
PANEL13  = "RACFCLSD"    /* Add class                      */ /* @AG */
PANELM2  = "RACFMSG2"    /* Display RACF command and RC    */ /* @AG */
EDITMACR = "RACFEMAC"    /* Edit Macro, turn HILITE off    */ /* @AZ */

ADDRESS ISPEXEC                                               /* @A6 */
  Arg user
  selection = 0  /* something selected */
  "CONTROL ERRORS RETURN"                                     /* @AL */
  "VGET (ZSCREEN ERATRACE ERASHOW ERASINT ERADISP) ASIS"      /* @AR */
  If (eratrace <> 'NO') then do                               /* @B2 */
     parse source . . REXXPGM .                               /* @B2 */
     Say "*"COPIES("-",70)"*"                                 /* @B2 */
     Say "*"Center("Begin Program = "REXXPGM,70)"*"           /* @B2 */
     Say "*"COPIES("-",70)"*"                                 /* @B2 */
     if (eratrace <> 'PROGRAMS') THEN                         /* @B4 */
        interpret "Trace "SUBSTR(ERATRACE,1,1)                /* @B2 */
  end                                                         /* @B2 */
  rlv = SYSVAR('SYSLRACF')

  seconds = time('S')
  call Select_class
  if selection then rc = display_table_permit()
  "TBEND" tblb

  If (eratrace <> 'NO') then do                               /* @B2 */
     Say "*"COPIES("-",70)"*"                                 /* @B2 */
     Say "*"Center("End Program = "REXXPGM,70)"*"             /* @B2 */
     Say "*"COPIES("-",70)"*"                                 /* @B2 */
  end                                                         /* @B2 */
EXIT
/*--------------------------------------------------------------------*/
/*  Get permits                                                       */
/*--------------------------------------------------------------------*/
GET_PERMITS:
  tblb    = 'TB'ZSCREEN||SECONDS  /* table name unique */     /* @AO */
  "TBCREATE" tblb" KEYS(CLASS PROFILE) REPLACE NOWRITE"       /* @AO */
  class = arg(1)
  user  = arg(2)
  cmd   = "SEARCH CLASS("class") USER("user")"                /* @A5 */
  x = OUTTRAP('VAR.')
  address TSO cmd                                             /* @A5 */
  cmd_rc = rc                                                 /* @AK */
  x = OUTTRAP('OFF')
  if (erashow <> 'NO') then                                   /* @AU */
     call SHOWCMD                                             /* @A5 */
  Do i = 1 to var.0   /* Scan output */
     profile = subword(var.i,1,1)
     If (profile = "NO") then                                 /* @AN */
        LEAVE         /* NO ENTRIES MEET SEARCH CRITERIA */
     if (profile <> 'ICH31005I') then do
        IF (ERASINT <> 0) THEN DO     /* Status interval */   /* @AP */
           IF (I//ERASINT = 0) THEN DO                        /* @AP */
              n1 = i; n2 = var.0
              "control display lock"
              "display msg(RACF012)"
           end                                                /* @AP */
        end                                                   /* @AP */
        "TBMOD" tblb
     end
  end /* end_do i=1     */
RETURN 0
/*--------------------------------------------------------------------*/
/*  Display permit table                                              */
/*--------------------------------------------------------------------*/
DISPLAY_TABLE_PERMIT:
  opta     = ' '
  sort     = 'PROFILE,C,A'                                    /* @AO */
  sortprof = 'D'                                              /* @AW */
  "TBSORT  "tblb" FIELDS("SORT")"                             /* @AO */
  "TBTOP   "tblb
  "TBDISPL "tblb "PANEL("PANEL08")"                           /* @AG */
  reta = rc
  do while (rc < 8)  /* tablea */
     "CONTROL DISPLAY SAVE"
     xtdtop = ztdtop
     lparm  = ""                                              /* @AO */
     PARSE VAR ZCMD ZCMD PARM SEQ                             /* @AO */
     IF (SROW <> "") & (SROW <> 0) THEN                       /* @A9 */
        IF (SROW > 0) THEN DO                                 /* @A9 */
           "TBTOP " TBLB                                      /* @A9 */
           "TBSKIP" TBLB "NUMBER("SROW")"                     /* @A9 */
        END                                                   /* @A9 */
     Select
        WHEN (ABBREV("SORT",ZCMD,1) = 1) THEN                 /* @AO */
             call sortseq 'PROFILE'                           /* @AW */
        WHEN (ABBREV("LOCATE",ZCMD,1) = 1) THEN               /* @AO */
             lparm = parm                                     /* @AO */
        WHEN (ABBREV("ONLY",ZCMD,1) = 1) THEN DO              /* @AO */
             find_str = translate(parm)                       /* @AO */
             'tbtop ' tblb                                    /* @AO */
             'tbskip' tblb                                    /* @AO */
             do forever                                       /* @AO */
                str = translate(profile)                      /* @AO */
                if (pos(find_str,str) > 0) then nop           /* @AO */
                else 'tbdelete' tblb                          /* @AO */
                'tbskip' tblb                                 /* @AO */
                if (rc > 0) then do                           /* @AO */
                   'tbtop' tblb                               /* @AO */
                   leave                                      /* @AO */
                end                                           /* @AO */
             end                                              /* @AO */
        END                                                   /* @AO */
        WHEN (ABBREV("RESET",ZCMD,1) = 1) THEN DO             /* @AO */
             sort     = 'PROFILE,C,A'                         /* @AO */
             sortprof = 'D'                                   /* @AW */
             xtdtop   = 1                                     /* @AO */
             rc = get_permits(class,user) /*get permits*/     /* @AO */
        END                                                   /* @AO */
        otherwise locvar=''
     End
     Select
        when (opta = 'L') then call lisd                      /* @AS */
        when (opta = 'S') then
             x = RACFCLSA(class profile user)
        otherwise nop
     End
     Do while (reta = 4)   /* process multi selection */
        "CONTROL DISPLAY RESTORE"
        "TBDISPL" tblb
        reta = rc
        "CONTROL DISPLAY SAVE"
        Select
           when (opta = 'S') then
                x = RACFCLSA(class profile user)              /* @B5 */
           otherwise nop
        End
     end
     opta = ' '
     "TBSORT "tblb" FIELDS("SORT")"                           /* @AQ */
     "TBTOP  "tblb                                            /* @AO */
     if (lparm <> '') then do
        ASTERICK = "*"                                        /* @AO */
        LOCARG   = "PROFILE"                                  /* @AO */
        INTERPRET LOCARG" = lparm||ASTERICK"                  /* @AO */
        "TBSCAN "tblb,                                        /* @AO */
                "ARGLIST("LOCARG") CONDLIST(GE)"              /* @AO */
     end
     else
        "TBSKIP" tblb "NUMBER("XTDTOP")"
     "CONTROL DISPLAY RESTORE"
     "TBDISPL" tblb "PANEL("PANEL08")"                        /* @AG */
     reta = rc
  end  /* Do while (reta < 8) */
RETURN 0
/*--------------------------------------------------------------------*/
/*  Define sort sequence, to allow point-n-shoot sorting (A/D)   @AW  */
/*--------------------------------------------------------------------*/
SORTSEQ:                                                      /* @AW */
  parse arg sortcol                                           /* @AW */
  INTERPRET "TMPSEQ = SORT"substr(SORTCOL,1,4)                /* @AW */
  select                                                      /* @AW */
     when (seq <> "") then do                                 /* @AW */
          if (seq = 'A') then                                 /* @AW */
             tmpseq = 'D'                                     /* @AW */
          else                                                /* @AW */
             tmpseq = 'A'                                     /* @AW */
          sort = sortcol',C,'seq                              /* @AW */
     end                                                      /* @AW */
     when (seq = ""),                                         /* @AW */
        & (tmpseq = 'A') then do                              /* @AW */
           sort   = sortcol',C,A'                             /* @AW */
           tmpseq = 'D'                                       /* @AW */
     end                                                      /* @AW */
     Otherwise do                                             /* @AW */
        sort   = sortcol',C,D'                                /* @AW */
        tmpseq = 'A'                                          /* @AW */
     end                                                      /* @AW */
  end                                                         /* @AW */
  INTERPRET "SORT"SUBSTR(SORTCOL,1,4)" = TMPSEQ"              /* @AW */
RETURN                                                        /* @AW */
/*--------------------------------------------------------------------*/
/*  Select class                                                      */
/*--------------------------------------------------------------------*/
SELECT_CLASS:
  call get_act_class
  rc = display_table()
  "TBEND" tbla
RETURN
/*--------------------------------------------------------------------*/
/*  Display profile permits                                           */
/*--------------------------------------------------------------------*/
GET_ACT_CLASS:
  seconds = time('S')                                         /* @AB */
  tbla    = 'TA'ZSCREEN||SECONDS  /* table name unique */     /* @AB */
  "TBCREATE" TBLA "KEYS(CLASS)",                              /* @AB */
                  "NAMES(ACTION) REPLACE NOWRITE"             /* @AB */
  Scan = 'OFF'                                                /* @AB */
  cmd  = "SETROPTS LIST"                                      /* @A5 */
  x = OUTTRAP('var.')
  address TSO cmd                                             /* @A5 */
  cmd_rc = rc                                                 /* @AK */
  x = OUTTRAP('OFF')
  if (erashow <> 'NO') then                                   /* @AU */
     call SHOWCMD                                             /* @A5 */
  Do i = 1 to var.0           /* Scan output */
     temp = var.i
     if (rlv > '1081') then   /* RACF 1.9 add blank */
        temp = ' 'temp
     Select
        when (substr(temp,2,16) = 'ACTIVE CLASSES =') then do
             scan   = 'ON'
             record = substr(temp,18,80)
             nwords = words(record)
             do t = 1 to nwords
                class = subword(record,t,1)
                if (class <> 'USER'),
                 & (class <> 'DATASET'),
                 & (class <> 'GROUP') then
                   "TBMOD" tbla
             end
        end
        when (substr(temp,2,25) =,
             'GENERIC PROFILE CLASSES =') then leave
        otherwise
             if (scan = 'ON') then do
                  record = var.i
                  nwords = words(record)
                  do t = 1 to nwords
                     class = subword(record,t,1)
                     if (class <> 'USER'),
                      & (class <> 'DATASET'),
                      & (class <> 'GROUP') then
                        "TBMOD" tbla
                  end
             end
     End    /* end_select */
  end  /* end_scan output */
RETURN
/*--------------------------------------------------------------------*/
/*  Display table                                                     */
/*--------------------------------------------------------------------*/
DISPLAY_TABLE:
  opta     = ' '
  sort     = 'CLASS,C,A'                                      /* @AD */
  sortclas = 0                                                /* @AW */
  "TBSORT " tbla "FIELDS("sort")"                             /* @AD */
  "TBTOP  " tbla
  "TBDISPL" tbla "PANEL("PANEL13")"                           /* @AG */
  reta = rc
  Do while ^selection & (reta < 8)
     "CONTROL DISPLAY SAVE"
     action = ''
     lclass = ''                                              /* @AE */
     xtdtop = ztdtop
     lparm  = ""                                              /* @AB */
     PARSE VAR ZCMD ZCMD PARM SEQ                             /* @AB */
     IF (SROW <> "") & (SROW <> 0) THEN                       /* @A9 */
        IF (SROW > 0) THEN DO                                 /* @A9 */
           "TBTOP " TBLA                                      /* @A9 */
           "TBSKIP" TBLA "NUMBER("SROW")"                     /* @A9 */
        END                                                   /* @A9 */
     Select                                                   /* @AB */
        WHEN (ABBREV("SORT",ZCMD,1) = 1) THEN                 /* @AB */
             call sortseq 'CLASS'                             /* @AW */
        WHEN (ABBREV("LOCATE",ZCMD,1) = 1) THEN               /* @AB */
             lclass = parm                                    /* @AE */
        WHEN (ABBREV("ONLY",ZCMD,1) = 1) THEN DO              /* @AB */
             find_str = translate(parm)                       /* @AB */
             'tbtop ' tbla                                    /* @AB */
             'tbskip' tbla                                    /* @AB */
             do forever                                       /* @AB */
                str = translate(class)                        /* @AB */
                if (pos(find_str,str) > 0) then nop           /* @AB */
                else 'tbdelete' tbla                          /* @AB */
                'tbskip' tbla                                 /* @AB */
                if (rc > 0) then do                           /* @AB */
                   'tbtop' tbla                               /* @AB */
                   leave                                      /* @AB */
                end                                           /* @AB */
             end                                              /* @AB */
        END                                                   /* @AB */
        WHEN (ABBREV("RESET",ZCMD,1) = 1) THEN DO             /* @AB */
             call get_act_class                               /* @AB */
             sort     = 'CLASS,C,A'                           /* @AD */
             sortclas = 'A'                                   /* @AW */
             xtdtop   = 1                                     /* @AB */
        END                                                   /* @AB */
        otherwise NOP                                         /* @AB */
     END /* Select */                                         /* @AB */
     select
        when (opta = 'L') then call lisp                      /* @AR */
        when (opta = 'S') then do
             DISPCLS = CLASS                                  /* @AC */
             rc = get_permits(class,user) /*get permits*/
             selection = 1
        end
        otherwise nop
     End
     Do while (reta = 4)   /* process multi selection */
        "CONTROL DISPLAY RESTORE"
        "TBDISPL" tbla
        reta = rc
        "CONTROL DISPLAY SAVE"
        Select
           when (opta = 'L') then call lisp                   /* @AR */
           when (opta = 'S') then
                rc = get_permits(class,user) /*get permits*/
           otherwise nop
        End
     end
     opta = ' '
     "TBSORT" tbla "FIELDS("sort")"                           /* @AB */
     "TBTOP " tbla
     if (lclass <> '') then do                                /* @AE */
        class = lclass                                        /* @AE */
        "TBSARG" TBLA "NEXT NAMECOND(CLASS,GE)"               /* @AE */
        "TBSCAN" TBLA "NOREAD"                                /* @AE */
     end                                                      /* @AE */
     else                                                     /* @AB */
        "TBSKIP" tbla "NUMBER("XTDTOP")"
     "CONTROL DISPLAY RESTORE"
     if ^selection then do
        "TBDISPL" tbla "PANEL("PANEL13")"                     /* @AG */
        reta = rc
     end
  end  /* Do while (reta < 8) */
RETURN 0
/*--------------------------------------------------------------------*/
/*  List class                                                   @AR  */
/*--------------------------------------------------------------------*/
LISP:                                                         /* @AR */
  DDNAME  = "RACFA"RANDOM(0,999)                              /* @AR */
  cmd     = "RL "CLASS" * ALL"                                /* @AT */
  X = OUTTRAP("CMDREC.")                                      /* @AR */
  ADDRESS TSO cmd                                             /* @AR */
  cmd_rc = rc                                                 /* @AR */
  X = OUTTRAP("OFF")                                          /* @AR */
  if (erashow <> 'NO') then                                   /* @AU */
     call SHOWCMD                                             /* @AR */
  if (cmd_rc > 0) then do   /* Remove parms */                /* @AR */
     cmd     = "RL "CLASS" *"                                 /* @AT */
     X = OUTTRAP("CMDREC.")                                   /* @AR */
     ADDRESS TSO cmd                                          /* @AR */
     cmd_rc = rc                                              /* @AR */
     X = OUTTRAP("OFF")                                       /* @AR */
     if (erashow <> 'NO') then                                /* @AU */
        call SHOWCMD                                          /* @AR */
  end                                                         /* @AR */
  call display_info                                           /* @A8 */
  if (cmd_rc > 0) then                                        /* @AR */
     CALL racfmsgs "ERR10" /* Generic failure */              /* @AR */
RETURN                                                        /* @AR */
/*--------------------------------------------------------------------*/
/*  Display information from line commands 'L' and 'P'           @AY  */
/*--------------------------------------------------------------------*/
DISPLAY_INFO:                                                 /* @AY */
  ADDRESS TSO "ALLOC F("DDNAME") NEW REUSE",                  /* @AR */
              "LRECL(80) BLKSIZE(0) RECFM(F B)",              /* @B1 */
              "UNIT(VIO) SPACE(1 5) CYLINDERS"                /* @AR */
  ADDRESS TSO "EXECIO * DISKW "DDNAME" (STEM CMDREC. FINIS"   /* @AR */
  "LMINIT DATAID(CMDDATID) DDNAME("DDNAME")"                  /* @AR */
  SELECT                                                      /* @AR */
     WHEN (ERADISP = "VIEW") THEN                             /* @AR */
          "VIEW DATAID("CMDDATID") MACRO("EDITMACR")"         /* @AX */
     WHEN (ERADISP = "EDIT") THEN                             /* @AR */
          "EDIT DATAID("CMDDATID") MACRO("EDITMACR")"         /* @AX */
     OTHERWISE                                                /* @AR */
          "BROWSE DATAID("CMDDATID")"                         /* @AR */
  END                                                         /* @AR */
  ADDRESS TSO "FREE FI("DDNAME")"                             /* @AR */
  DROP CMDREC. DDNAME CMDPRM                                  /* @AY */
RETURN                                                        /* @AY */
/*--------------------------------------------------------------------*/
/*  List class's profile                                         @AS  */
/*--------------------------------------------------------------------*/
LISD:                                                         /* @AS */
  DDNAME = "ERA"RANDOM(0,99999)                               /* @AS */
  cmd  = "RLIST "CLASS PROFILE" AUTH"                         /* @AS */
  X    = OUTTRAP("CMDREC.")                                   /* @AS */
  ADDRESS TSO cmd                                             /* @AS */
  cmd_rc = rc                                                 /* @AS */
  X    = OUTTRAP("OFF")                                       /* @AS */
  if (erashow <> 'NO') then                                   /* @AU */
     call SHOWCMD                                             /* @AS */
  if (cmd_rc > 0) then do    /* Remove parms */               /* @AS */
     cmd = "RLIST "RCLASS PROFILE                             /* @AS */
     X = OUTTRAP("CMDREC.")                                   /* @AS */
     ADDRESS TSO cmd                                          /* @AS */
     cmd_rc = rc                                              /* @AS */
     X = OUTTRAP("OFF")                                       /* @AS */
     if (erashow <> 'NO') then                                /* @AU */
        call SHOWCMD                                          /* @AS */
  end                                                         /* @AS */
  call display_info                                           /* @A8 */
  if (cmd_rc > 0) then                                        /* @AS */
     CALL racfmsgs "ERR10" /* Generic failure */              /* @AS */
RETURN                                                        /* @AS */
/*--------------------------------------------------------------------*/
/*  Display RACF command and return code                         @AV  */
/*--------------------------------------------------------------------*/
SHOWCMD:                                                      /* @AV */
  IF (ERASHOW = "BOTH") | (ERASHOW = "DISPLAY") THEN DO       /* @AV */
     PARSE VAR CMD MSG1 60 MSG2 121 MSG3                      /* @AV */
     MSG4 = "Return code = "cmd_rc                            /* @AK */
     "ADDPOP ROW(6) COLUMN(4)"                                /* @AH */
     "DISPLAY PANEL("PANELM2")"                               /* @AG */
     "REMPOP"                                                 /* @AH */
  END                                                         /* @AU */
  IF (ERASHOW = "BOTH") | (ERASHOW = "LOG") THEN DO           /* @AV */
     zerrsm = "RACFADM RACFCLSS RC="cmd_rc                    /* @AU */
     zerrlm = cmd                                             /* @AU */
     'log msg(isrz003)'                                       /* @AU */
  END                                                         /* @AU */
RETURN                                                        /* @AV */
