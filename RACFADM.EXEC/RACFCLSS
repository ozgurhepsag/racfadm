/*%NOCOMMENT====================* REXX *==============================*/
/*  PURPOSE:  RACFADM - General resources - Search classes            */
/*--------------------------------------------------------------------*/
/* FLG  YYMMDD  USERID   DESCRIPTION                                  */
/* ---  ------  -------  -------------------------------------------- */
/* @CA  200402  RACFA    Chg LRECL=132 to LRECL=80                    */
/* @C9  200402  RACFA    Fixed VIEW/EDIT, was missing edit macro var. */
/* @C8  200401  RACFA    Create subroutine to VIEW/EDIT/BROWSE        */
/* @C7  200401  RACFA    VIEW/EDIT use edit macro, to turn off HILITE */
/* @C6  200330  RACFA    Allow point-n-shoot sort ascending/descending*/
/* @C5  200324  RACFA    Allow both display/logging of RACF commands  */
/* @C4  200324  RACFA    Allow logging RACF commands to ISPF Log file */
/* @C3  200303  RACFA    Chg 'RL class ALL' to 'RL class * ALL'       */
/* @C2  200303  RACFA    Added line cmd 'L-List' to class's profile   */
/* @C1  200303  RACFA    Added line cmd 'L-List' to class             */
/* @BZ  200302  RACFA    Fixed SORT cmd for class profiles            */
/* @BY  200302  RACFA    Chk/honor 'Status Interval' for cls profiles */
/* @BX  200302  RACFA    Add SORT/LOCATE/ONLY/RESET for class profiles*/
/* @BW  200301  RACFA    Chk for 'NO ENTRIES MEET SEARCH CRITERIA'    */
/* @BU  200226  RACFA    Fix @AK chg, chg ret_code to cmd_rc          */
/* @AL  200226  RACFA    Added 'CONTROL ERRORS RETURN'                */
/* @AK  200226  RACFA    Removed double quotes before/after cmd       */
/* @AJ  200225  RACFA    Del address TSO "PROFILE PREF("USERID()")"   */
/* @AI  200224  RACFA    Fixed PANEL08, typo in defining variable     */
/* @AH  200224  RACFA    Standardize quotes, chg single to double     */
/* @AG  200224  RACFA    Place panels at top of REXX in variables     */
/* @AF  200223  RACFA    Del 'address TSO "PROFILE MSGID"', not needed*/
/* @AE  200223  RACFA    Reduced code when LOCATEing, not necessary   */
/* @AD  200223  RACFA    Reduced code when SORTing, not necessary     */
/* @AC  200223  RACFA    Created RACFCLSD (only Class, no profile col)*/
/* @AB  200223  RACFA    Added primary cmds: SORT, LOCATE, ONLY, RESET*/
/* @AA  200222  RACFA    Removed translating OPTA/B, not needed       */
/* @A9  200222  RACFA    Allow placing cursor on row and press ENTER  */
/* @A8  200221  RACFA    Removed "G = '(G)'", not referenced          */
/* @A7  200221  RACFA    Get ERASHOW, to display RACF cmds            */
/* @A6  200221  RACFA    Make 'ADDRESS ISPEXEC' defualt, reduce code  */
/* @A5  200220  RACFA    Fixed displaying all RACF commands           */
/* @A4  200220  RACFA    Added ERATRACE=YES, then TRACE R             */
/* @A3  200214  RACFA    Chged panel name RACFSRCH to RACFCLS8        */
/* @A2  200120  RACFA    Standardized/reduced lines of code           */
/* @A1  200120  RACFA    Added comment box above procedures           */
/* @A0  011229  NICORIZ  Created REXX, V2.1, www.rizzuto.it           */
/*====================================================================*/
PANEL08  = "RACFCLS8"    /* List classes and profiles      */ /* @AI */
PANEL13  = "RACFCLSD"    /* Add class                      */ /* @AG */
PANELM2  = "RACFMSG2"    /* Display RACF command and RC    */ /* @AG */
EDITMACR = "RACFEMAC"                                         /* @C9 */

ADDRESS ISPEXEC                                               /* @A6 */
  Arg user
  selection = 0  /* something selected */
  "CONTROL ERRORS RETURN"                                     /* @AL */
  "VGET (ZSCREEN ERATRACE ERASHOW ERASINT ERADISP) ASIS"      /* @C1 */
  If (eratrace = 'YES') then Trace r                          /* @A4 */
  rlv = SYSVAR('SYSLRACF')

  seconds = time('S')
  call Select_class
  if selection then rc = display_table_permit()
  "TBEND" tblb
EXIT
/*--------------------------------------------------------------------*/
/*  Get permits                                                       */
/*--------------------------------------------------------------------*/
GET_PERMITS:
  tblb    = 'TB'ZSCREEN||SECONDS  /* table name unique */     /* @BX */
  "TBCREATE" tblb" KEYS(CLASS PROFILE) REPLACE NOWRITE"       /* @BX */
  class = arg(1)
  user  = arg(2)
  cmd   = "SEARCH CLASS("class") USER("user")"                /* @A5 */
  x = OUTTRAP('VAR.')
  address TSO cmd                                             /* @A5 */
  cmd_rc = rc                                                 /* @AK */
  x = OUTTRAP('OFF')
  if (erashow <> 'NO') then                                   /* @C4 */
     call SHOWCMD                                             /* @A5 */
  Do i = 1 to var.0   /* Scan output */
     profile = subword(var.i,1,1)
     If (profile = "NO") then                                 /* @BW */
        LEAVE         /* NO ENTRIES MEET SEARCH CRITERIA */
     if (profile <> 'ICH31005I') then do
        IF (ERASINT <> 0) THEN DO     /* Status interval */   /* @BY */
           IF (I//ERASINT = 0) THEN DO                        /* @BY */
              n1 = i; n2 = var.0
              "control display lock"
              "display msg(RACF012)"
           end                                                /* @BY */
        end                                                   /* @BY */
        "TBMOD" tblb
     end
  end /* end_do i=1     */
RETURN 0
/*--------------------------------------------------------------------*/
/*  Display permit table                                              */
/*--------------------------------------------------------------------*/
DISPLAY_TABLE_PERMIT:
  opta     = ' '
  sort     = 'PROFILE,C,A'                                    /* @BX */
  sortprof = 'D'                                              /* @C6 */
  "TBSORT  "tblb" FIELDS("SORT")"                             /* @BX */
  "TBTOP   "tblb
  "TBDISPL "tblb "PANEL("PANEL08")"                           /* @AG */
  reta = rc
  do while (rc < 8)  /* tablea */
     "CONTROL DISPLAY SAVE"
     xtdtop = ztdtop
     lparm  = ""                                              /* @BX */
     PARSE VAR ZCMD ZCMD PARM SEQ                             /* @BX */
     IF (SROW <> "") & (SROW <> 0) THEN                       /* @A9 */
        IF (SROW > 0) THEN DO                                 /* @A9 */
           "TBTOP " TBLB                                      /* @A9 */
           "TBSKIP" TBLB "NUMBER("SROW")"                     /* @A9 */
        END                                                   /* @A9 */
     Select
        WHEN (ABBREV("SORT",ZCMD,1) = 1) THEN                 /* @BX */
             call sortseq 'PROFILE'                           /* @C6 */
        WHEN (ABBREV("LOCATE",ZCMD,1) = 1) THEN               /* @BX */
             lparm = parm                                     /* @BX */
        WHEN (ABBREV("ONLY",ZCMD,1) = 1) THEN DO              /* @BX */
             find_str = translate(parm)                       /* @BX */
             'tbtop ' tblb                                    /* @BX */
             'tbskip' tblb                                    /* @BX */
             do forever                                       /* @BX */
                str = translate(profile)                      /* @BX */
                if (pos(find_str,str) > 0) then nop           /* @BX */
                else 'tbdelete' tblb                          /* @BX */
                'tbskip' tblb                                 /* @BX */
                if (rc > 0) then do                           /* @BX */
                   'tbtop' tblb                               /* @BX */
                   leave                                      /* @BX */
                end                                           /* @BX */
             end                                              /* @BX */
        END                                                   /* @BX */
        WHEN (ABBREV("RESET",ZCMD,1) = 1) THEN DO             /* @BX */
             sort     = 'PROFILE,C,A'                         /* @BX */
             sortprof = 'D'                                   /* @C6 */
             xtdtop   = 1                                     /* @BX */
             rc = get_permits(class,user) /*get permits*/     /* @BX */
        END                                                   /* @BX */
        otherwise locvar=''
     End
     Select
        when (opta = 'L') then call lisd                      /* @C2 */
        when (opta = 'S') then x = RACFAUTH(class profile user)
        otherwise nop
     End
     Do while (reta = 4)   /* process multi selection */
        "CONTROL DISPLAY RESTORE"
        "TBDISPL" tblb
        reta = rc
        "CONTROL DISPLAY SAVE"
        Select
           when (opta = 'S') then
                x = RACFAUTH(class profile user)
           otherwise nop
        End
     end
     opta = ' '
     "TBSORT "tblb" FIELDS("SORT")"                           /* @BZ */
     "TBTOP  "tblb                                            /* @BX */
     if (lparm <> '') then do
        ASTERICK = "*"                                        /* @BX */
        LOCARG   = "PROFILE"                                  /* @BX */
        INTERPRET LOCARG" = lparm||ASTERICK"                  /* @BX */
        "TBSCAN "tblb,                                        /* @BX */
                "ARGLIST("LOCARG") CONDLIST(GE)"              /* @BX */
     end
     else
        "TBSKIP" tblb "NUMBER("XTDTOP")"
     "CONTROL DISPLAY RESTORE"
     "TBDISPL" tblb "PANEL("PANEL08")"                        /* @AG */
     reta = rc
  end  /* Do while (reta < 8) */
RETURN 0
/*--------------------------------------------------------------------*/
/*  Define sort sequence, to allow point-n-shoot sorting (A/D)   @C6  */
/*--------------------------------------------------------------------*/
SORTSEQ:                                                      /* @C6 */
  parse arg sortcol                                           /* @C6 */
  INTERPRET "TMPSEQ = SORT"substr(SORTCOL,1,4)                /* @C6 */
  select                                                      /* @C6 */
     when (seq <> "") then do                                 /* @C6 */
          if (seq = 'A') then                                 /* @C6 */
             tmpseq = 'D'                                     /* @C6 */
          else                                                /* @C6 */
             tmpseq = 'A'                                     /* @C6 */
          sort = sortcol',C,'seq                              /* @C6 */
     end                                                      /* @C6 */
     when (seq = ""),                                         /* @C6 */
        & (tmpseq = 'A') then do                              /* @C6 */
           sort   = sortcol',C,A'                             /* @C6 */
           tmpseq = 'D'                                       /* @C6 */
     end                                                      /* @C6 */
     Otherwise do                                             /* @C6 */
        sort   = sortcol',C,D'                                /* @C6 */
        tmpseq = 'A'                                          /* @C6 */
     end                                                      /* @C6 */
  end                                                         /* @C6 */
  INTERPRET "SORT"SUBSTR(SORTCOL,1,4)" = TMPSEQ"              /* @C6 */
RETURN                                                        /* @C6 */
/*--------------------------------------------------------------------*/
/*  Select class                                                      */
/*--------------------------------------------------------------------*/
SELECT_CLASS:
  call get_act_class
  rc = display_table()
  "TBEND" tbla
RETURN
/*--------------------------------------------------------------------*/
/*  Display profile permits                                           */
/*--------------------------------------------------------------------*/
GET_ACT_CLASS:
  seconds = time('S')                                         /* @AB */
  tbla    = 'TA'ZSCREEN||SECONDS  /* table name unique */     /* @AB */
  "TBCREATE" TBLA "KEYS(CLASS)",                              /* @AB */
                  "NAMES(ACTION) REPLACE NOWRITE"             /* @AB */
  Scan = 'OFF'                                                /* @AB */
  cmd  = "SETROPTS LIST"                                      /* @A5 */
  x = OUTTRAP('var.')
  address TSO cmd                                             /* @A5 */
  cmd_rc = rc                                                 /* @AK */
  x = OUTTRAP('OFF')
  if (erashow <> 'NO') then                                   /* @C4 */
     call SHOWCMD                                             /* @A5 */
  Do i = 1 to var.0           /* Scan output */
     temp = var.i
     if (rlv > '1081') then   /* RACF 1.9 add blank */
        temp = ' 'temp
     Select
        when (substr(temp,2,16) = 'ACTIVE CLASSES =') then do
             scan   = 'ON'
             record = substr(temp,18,80)
             nwords = words(record)
             do t = 1 to nwords
                class = subword(record,t,1)
                if (class <> 'USER'),
                 & (class <> 'DATASET'),
                 & (class <> 'GROUP') then
                   "TBMOD" tbla
             end
        end
        when (substr(temp,2,25) =,
             'GENERIC PROFILE CLASSES =') then leave
        otherwise
             if (scan = 'ON') then do
                  record = var.i
                  nwords = words(record)
                  do t = 1 to nwords
                     class = subword(record,t,1)
                     if (class <> 'USER'),
                      & (class <> 'DATASET'),
                      & (class <> 'GROUP') then
                        "TBMOD" tbla
                  end
             end
     End    /* end_select */
  end  /* end_scan output */
RETURN
/*--------------------------------------------------------------------*/
/*  Display table                                                     */
/*--------------------------------------------------------------------*/
DISPLAY_TABLE:
  opta     = ' '
  sort     = 'CLASS,C,A'                                      /* @AD */
  sortclas = 0                                                /* @C6 */
  "TBSORT " tbla "FIELDS("sort")"                             /* @AD */
  "TBTOP  " tbla
  "TBDISPL" tbla "PANEL("PANEL13")"                           /* @AG */
  reta = rc
  Do while ^selection & (reta < 8)
     "CONTROL DISPLAY SAVE"
     action = ''
     lclass = ''                                              /* @AE */
     xtdtop = ztdtop
     lparm  = ""                                              /* @AB */
     PARSE VAR ZCMD ZCMD PARM SEQ                             /* @AB */
     IF (SROW <> "") & (SROW <> 0) THEN                       /* @A9 */
        IF (SROW > 0) THEN DO                                 /* @A9 */
           "TBTOP " TBLA                                      /* @A9 */
           "TBSKIP" TBLA "NUMBER("SROW")"                     /* @A9 */
        END                                                   /* @A9 */
     Select                                                   /* @AB */
        WHEN (ABBREV("SORT",ZCMD,1) = 1) THEN                 /* @AB */
             call sortseq 'CLASS'                             /* @C6 */
        WHEN (ABBREV("LOCATE",ZCMD,1) = 1) THEN               /* @AB */
             lclass = parm                                    /* @AE */
        WHEN (ABBREV("ONLY",ZCMD,1) = 1) THEN DO              /* @AB */
             find_str = translate(parm)                       /* @AB */
             'tbtop ' tbla                                    /* @AB */
             'tbskip' tbla                                    /* @AB */
             do forever                                       /* @AB */
                str = translate(class)                        /* @AB */
                if (pos(find_str,str) > 0) then nop           /* @AB */
                else 'tbdelete' tbla                          /* @AB */
                'tbskip' tbla                                 /* @AB */
                if (rc > 0) then do                           /* @AB */
                   'tbtop' tbla                               /* @AB */
                   leave                                      /* @AB */
                end                                           /* @AB */
             end                                              /* @AB */
        END                                                   /* @AB */
        WHEN (ABBREV("RESET",ZCMD,1) = 1) THEN DO             /* @AB */
             call get_act_class                               /* @AB */
             sort     = 'CLASS,C,A'                           /* @AD */
             sortclas = 'A'                                   /* @C6 */
             xtdtop   = 1                                     /* @AB */
        END                                                   /* @AB */
        otherwise NOP                                         /* @AB */
     END /* Select */                                         /* @AB */
     select
        when (opta = 'L') then call lisp                      /* @C1 */
        when (opta = 'S') then do
             DISPCLS = CLASS                                  /* @AC */
             rc = get_permits(class,user) /*get permits*/
             selection = 1
        end
        otherwise nop
     End
     Do while (reta = 4)   /* process multi selection */
        "CONTROL DISPLAY RESTORE"
        "TBDISPL" tbla
        reta = rc
        "CONTROL DISPLAY SAVE"
        Select
           when (opta = 'L') then call lisp                   /* @C1 */
           when (opta = 'S') then
                rc = get_permits(class,user) /*get permits*/
           otherwise nop
        End
     end
     opta = ' '
     "TBSORT" tbla "FIELDS("sort")"                           /* @AB */
     "TBTOP " tbla
     if (lclass <> '') then do                                /* @AE */
        class = lclass                                        /* @AE */
        "TBSARG" TBLA "NEXT NAMECOND(CLASS,GE)"               /* @AE */
        "TBSCAN" TBLA "NOREAD"                                /* @AE */
     end                                                      /* @AE */
     else                                                     /* @AB */
        "TBSKIP" tbla "NUMBER("XTDTOP")"
     "CONTROL DISPLAY RESTORE"
     if ^selection then do
        "TBDISPL" tbla "PANEL("PANEL13")"                     /* @AG */
        reta = rc
     end
  end  /* Do while (reta < 8) */
RETURN 0
/*--------------------------------------------------------------------*/
/*  List class                                                   @C1  */
/*--------------------------------------------------------------------*/
LISP:                                                         /* @C1 */
  DDNAME  = "ERA"RANDOM(0,99999)                              /* @C1 */
  cmd     = "RL "CLASS" * ALL"                                /* @C3 */
  X = OUTTRAP("CMDREC.")                                      /* @C1 */
  ADDRESS TSO cmd                                             /* @C1 */
  cmd_rc = rc                                                 /* @C1 */
  X = OUTTRAP("OFF")                                          /* @C1 */
  if (erashow <> 'NO') then                                   /* @C4 */
     call SHOWCMD                                             /* @C1 */
  if (cmd_rc > 0) then do   /* Remove parms */                /* @C1 */
     cmd     = "RL "CLASS" *"                                 /* @C3 */
     X = OUTTRAP("CMDREC.")                                   /* @C1 */
     ADDRESS TSO cmd                                          /* @C1 */
     cmd_rc = rc                                              /* @C1 */
     X = OUTTRAP("OFF")                                       /* @C1 */
     if (erashow <> 'NO') then                                /* @C4 */
        call SHOWCMD                                          /* @C1 */
  end                                                         /* @C1 */
  call display_info                                           /* @A8 */
  if (cmd_rc > 0) then                                        /* @C1 */
     CALL racfmsgs "ERR10" /* Generic failure */              /* @C1 */
RETURN                                                        /* @C1 */
/*--------------------------------------------------------------------*/
/*  Display information from line commands 'L' and 'P'           @C8  */
/*--------------------------------------------------------------------*/
DISPLAY_INFO:                                                 /* @C8 */
  ADDRESS TSO "ALLOC F("DDNAME") NEW REUSE",                  /* @C1 */
              "LRECL(80) BLKSIZE(0) RECFM(F B)",              /* @CA */
              "UNIT(VIO) SPACE(1 5) CYLINDERS"                /* @C1 */
  ADDRESS TSO "EXECIO * DISKW "DDNAME" (STEM CMDREC. FINIS"   /* @C1 */
  "LMINIT DATAID(CMDDATID) DDNAME("DDNAME")"                  /* @C1 */
  SELECT                                                      /* @C1 */
     WHEN (ERADISP = "VIEW") THEN                             /* @C1 */
          "VIEW DATAID("CMDDATID") MACRO("EDITMACR")"         /* @C7 */
     WHEN (ERADISP = "EDIT") THEN                             /* @C1 */
          "EDIT DATAID("CMDDATID") MACRO("EDITMACR")"         /* @C7 */
     OTHERWISE                                                /* @C1 */
          "BROWSE DATAID("CMDDATID")"                         /* @C1 */
  END                                                         /* @C1 */
  ADDRESS TSO "FREE FI("DDNAME")"                             /* @C1 */
  DROP CMDREC. DDNAME CMDPRM                                  /* @C8 */
RETURN                                                        /* @C8 */
/*--------------------------------------------------------------------*/
/*  List class's profile                                         @C2  */
/*--------------------------------------------------------------------*/
LISD:                                                         /* @C2 */
  DDNAME = "ERA"RANDOM(0,99999)                               /* @C2 */
  cmd  = "RLIST "CLASS PROFILE" AUTH"                         /* @C2 */
  X    = OUTTRAP("CMDREC.")                                   /* @C2 */
  ADDRESS TSO cmd                                             /* @C2 */
  cmd_rc = rc                                                 /* @C2 */
  X    = OUTTRAP("OFF")                                       /* @C2 */
  if (erashow <> 'NO') then                                   /* @C4 */
     call SHOWCMD                                             /* @C2 */
  if (cmd_rc > 0) then do    /* Remove parms */               /* @C2 */
     cmd = "RLIST "RCLASS PROFILE                             /* @C2 */
     X = OUTTRAP("CMDREC.")                                   /* @C2 */
     ADDRESS TSO cmd                                          /* @C2 */
     cmd_rc = rc                                              /* @C2 */
     X = OUTTRAP("OFF")                                       /* @C2 */
     if (erashow <> 'NO') then                                /* @C4 */
        call SHOWCMD                                          /* @C2 */
  end                                                         /* @C2 */
  call display_info                                           /* @A8 */
  if (cmd_rc > 0) then                                        /* @C2 */
     CALL racfmsgs "ERR10" /* Generic failure */              /* @C2 */
RETURN                                                        /* @C2 */
/*--------------------------------------------------------------------*/
/*  Display RACF command and return code                         @C5  */
/*--------------------------------------------------------------------*/
SHOWCMD:                                                      /* @C5 */
  IF (ERASHOW = "BOTH") | (ERASHOW = "DISPLAY") THEN DO       /* @C5 */
     PARSE VAR CMD MSG1 60 MSG2 121 MSG3                      /* @C5 */
     MSG4 = "Return code = "cmd_rc                            /* @AK */
     "ADDPOP ROW(6) COLUMN(4)"                                /* @AH */
     "DISPLAY PANEL("PANELM2")"                               /* @AG */
     "REMPOP"                                                 /* @AH */
  END                                                         /* @C4 */
  IF (ERASHOW = "BOTH") | (ERASHOW = "LOG") THEN DO           /* @C5 */
     zerrsm = "RACFADM RACFCLSS RC="cmd_rc                    /* @C4 */
     zerrlm = cmd                                             /* @C4 */
     'log msg(isrz003)'                                       /* @C4 */
  END                                                         /* @C4 */
RETURN                                                        /* @C5 */
/*--------------------------------------------------------------------*/
/*  Error message                                                     */
/*--------------------------------------------------------------------*/
ERROR:
  say 'Error RACFCLSS at line 'sigl
EXIT
