/*%NOCOMMENT====================* REXX *==============================*/
/*  PURPOSE:  RACFADM - General resources - Search classes            */
/*--------------------------------------------------------------------*/
/* FLG  YYMMDD  USERID   DESCRIPTION                                  */
/* ---  ------  -------  -------------------------------------------- */
/* @AA  200222  RACFA    Removed translating OPTA/B, not needed       */
/* @A9  200222  RACFA    Allow placing cursor on row and press ENTER  */
/* @A8  200221  RACFA    Removed "G = '(G)'", not referenced          */
/* @A7  200221  RACFA    Get ERASHOW, to display RACF cmds            */
/* @A6  200221  RACFA    Make 'ADDRESS ISPEXEC' defualt, reduce code  */
/* @A5  200220  RACFA    Fixed displaying all RACF commands           */
/* @A4  200220  RACFA    Added ERATRACE=YES, then TRACE R             */
/* @A3  200214  RACFA    Chged panel name RACFSRCH to RACFCLS8        */
/* @A2  200120  RACFA    Standardized/reduced lines of code           */
/* @A1  200120  RACFA    Added comment box above procedures           */
/* @A0  011229  NICORIZ  Created REXX, V2.1, www.rizzuto.it           */
/*====================================================================*/
ADDRESS ISPEXEC                                               /* @A6 */
  Arg user
  selection = 0  /* something selected */
  "VGET (ZSCREEN ERATRACE ERASHOW) ASIS"                      /* @A7 */
  If (eratrace = 'YES') then Trace r                          /* @A4 */
  address TSO "PROFILE MSGID"
  address TSO "PROFILE PREF("USERID()")"
  rlv = SYSVAR('SYSLRACF')

  seconds = time('S')
  tblb    = 'TB'ZSCREEN||SECONDS  /* table name unique */
  "TBCREATE" tblb" KEYS(CLASS PROFILE) REPLACE NOWRITE"
  call Select_class
  if selection then rc = display_table_permit()
  "TBEND" tblb
EXIT
/*--------------------------------------------------------------------*/
/*  Get permits                                                       */
/*--------------------------------------------------------------------*/
GET_PERMITS:
  class = arg(1)
  user  = arg(2)
  cmd   = "SEARCH CLASS("class") USER("user")"                /* @A5 */
  if (erashow = 'YES') then                                   /* @A5 */
     call SHOWCMD                                             /* @A5 */
  x = OUTTRAP('VAR.')
  address TSO cmd                                             /* @A5 */
  x = OUTTRAP('OFF')
  Do i = 1 to var.0   /* Scan output */
     profile = subword(var.i,1,1)
     if (profile <> 'ICH31005I') then do
        n1 = i; n2 = var.0
        "control display lock"
        "display msg(RACF012)"
        "TBMOD" tblb
     end
  end /* end_do i=1     */
RETURN 0
/*--------------------------------------------------------------------*/
/*  Display permit table                                              */
/*--------------------------------------------------------------------*/
DISPLAY_TABLE_PERMIT:
 opta = ' '
 "TBSORT  "tblb" FIELDS(CLASS,C,A,PROFILE,C,A)"
 "TBTOP   "tblb
 "TBDISPL "tblb "PANEL(RACFCLS8)"                             /* @A3 */
 reta = rc
 do while (rc < 8)  /* tablea */
    "CONTROL DISPLAY SAVE"
    xtdtop = ztdtop
    IF (SROW <> "") & (SROW <> 0) THEN                        /* @A9 */
       IF (SROW > 0) THEN DO                                  /* @A9 */
          "TBTOP " TBLB                                       /* @A9 */
          "TBSKIP" TBLB "NUMBER("SROW")"                      /* @A9 */
       END                                                    /* @A9 */
    Select
       when (subword(ZCMD,1,1)  = 'LOCATE' |,
             subword(ZCMD,1,1)  = 'L') &,
             (subword(ZCMD,2,1) ='CLASS' |,
              subword(ZCMD,2,1) ='C') then do
              locvar  = 'CLASS'
              profile = ' '
              locval  = subword(ZCMD,3,1)
       end
       when (subword(ZCMD,1,1)  = 'LOCATE' |,
             subword(ZCMD,1,1)  = 'L') &,
             (subword(ZCMD,2,1) ='PROFILE' |,
              subword(ZCMD,2,1) ='P') then do
              locvar = 'PROFILE'
              locval = subword(ZCMD,3,1)
       end
       otherwise locvar=''
    End
    Select
       when (opta = 'S') then x = RACFAUTH(class profile user)
       otherwise nop
    End
    Do while (reta = 4)   /* process multi selection */
       "CONTROL DISPLAY RESTORE"
       "TBDISPL" tblb
       reta = rc
       "CONTROL DISPLAY SAVE"
       Select
          when (opta = 'S') then
               x = RACFAUTH(class profile user)
          otherwise nop
       End
    end
    opta = ' '
    "TBSORT "tblb" FIELDS(CLASS,C,A,PROFILE,C,A)"
    if (locvar <> '') then do
       interpret locvar'='locval
       "TBTOP " tblb
       "TBSARG" tblb "NEXT NAMECOND(CLASS,GE,PROFILE,GE)"
       "TBSCAN" tblb
    end
    else do
       "TBTOP " tblb
       "TBSKIP" tblb "NUMBER("XTDTOP")"
    end
    "CONTROL DISPLAY RESTORE"
    "TBDISPL" tblb "PANEL(RACFCLS8)"                          /* @A3 */
    reta = rc
 end  /* Do while (reta < 8) */
RETURN 0
/*--------------------------------------------------------------------*/
/*  Select class                                                      */
/*--------------------------------------------------------------------*/
SELECT_CLASS:
  seconds = time('S')
  tbla    = 'TA'ZSCREEN||SECONDS  /* table name unique */
  "TBCREATE" TBLA "KEYS(CLASS)",
                  "NAMES(ACTION) REPLACE NOWRITE"
  call get_act_class
  rc = display_table()
  "TBEND" tbla
RETURN
/*--------------------------------------------------------------------*/
/*  Display profile permits                                           */
/*--------------------------------------------------------------------*/
GET_ACT_CLASS:
  Scan = 'OFF'
  cmd  = "SETROPTS LIST"                                      /* @A5 */
  if (erashow = 'YES') then                                   /* @A5 */
     call SHOWCMD                                             /* @A5 */
  x = OUTTRAP('var.')
  address TSO cmd                                             /* @A5 */
  x = OUTTRAP('OFF')
  Do i = 1 to var.0           /* Scan output */
     temp = var.i
     if (rlv > '1081') then   /* RACF 1.9 add blank */
        temp = ' 'temp
     Select
        when (substr(temp,2,16) = 'ACTIVE CLASSES =') then do
             scan   = 'ON'
             record = substr(temp,18,80)
             nwords = words(record)
             do t = 1 to nwords
                class = subword(record,t,1)
                if (class <> 'USER'),
                 & (class <> 'DATASET'),
                 & (class <> 'GROUP') then
                   "TBMOD" tbla
             end
        end
        when (substr(temp,2,25) =,
             'GENERIC PROFILE CLASSES =') then leave
        otherwise
             if (scan = 'ON') then do
                  record = var.i
                  nwords = words(record)
                  do t = 1 to nwords
                     class = subword(record,t,1)
                     if (class <> 'USER'),
                      & (class <> 'DATASET'),
                      & (class <> 'GROUP') then
                        "TBMOD" tbla
                  end
             end
     End    /* end_select */
  end  /* end_scan output */
RETURN
/*--------------------------------------------------------------------*/
/*  Display table                                                     */
/*--------------------------------------------------------------------*/
DISPLAY_TABLE:
  opta = ' '
  "TBSORT " tbla "FIELDS(CLASS)"
  "TBTOP  " tbla
  "TBDISPL" tbla "PANEL(RACFCLS8)"                            /* @A3 */
  reta = rc
  Do while ^selection & (reta < 8)
     action = ''
     "CONTROL DISPLAY SAVE"
     IF (SROW <> "") & (SROW <> 0) THEN                       /* @A9 */
        IF (SROW > 0) THEN DO                                 /* @A9 */
           "TBTOP " TBLA                                      /* @A9 */
           "TBSKIP" TBLA "NUMBER("SROW")"                     /* @A9 */
        END                                                   /* @A9 */
     Select
        when (subword(ZCMD,1,1) = 'LOCATE') |,
             (subword(ZCMD,1,1) = 'L') then
             lclass = subword(ZCMD,2,1)
        otherwise lclass = ''
     End
     xtdtop = ztdtop
     select
        when (opta = 'S') then do
             rc = get_permits(class,user) /*get permits*/
             selection = 1
        end
        otherwise nop
     End
     Do while (reta = 4)   /* process multi selection */
        "CONTROL DISPLAY RESTORE"
        "TBDISPL" tbla
        reta = rc
        "CONTROL DISPLAY SAVE"
        Select
           when (opta = 'S') then
                rc = get_permits(class,user) /*get permits*/
           otherwise nop
        End
     end
     opta = ' '
     "TBSORT" tbla "FIELDS(CLASS)"
     "TBTOP " tbla
     if (lclass <> '') then do
        class = lclass
        "TBSARG" TBLA "NEXT NAMECOND(CLASS,GE)"
        "TBSCAN" TBLA "NOREAD"
     end
     else
        "TBSKIP" tbla "NUMBER("XTDTOP")"
     "CONTROL DISPLAY RESTORE"
     if ^selection then do
        "TBDISPL" tbla "PANEL(RACFCLS8)"                      /* @A3 */
        reta = rc
     end
  end  /* Do while (reta < 8) */
RETURN 0
/*--------------------------------------------------------------------*/
/*  Error message                                                     */
/*--------------------------------------------------------------------*/
ERROR:
  say 'Error RACFCLSS at line 'sigl
EXIT
/*--------------------------------------------------------------------*/
/*  Display RACF command                                          @A5 */
/*--------------------------------------------------------------------*/
SHOWCMD:                                                      /* @A5 */
  PARSE VAR CMD MSG1 60 MSG2 121 MSG3                         /* @A5 */
  'ADDPOP ROW(6) COLUMN(4)'                                   /* @A5 */
  'DISPLAY PANEL(RACFMSG2)'                                   /* @A5 */
  'REMPOP'                                                    /* @A5 */
RETURN                                                        /* @A5 */
